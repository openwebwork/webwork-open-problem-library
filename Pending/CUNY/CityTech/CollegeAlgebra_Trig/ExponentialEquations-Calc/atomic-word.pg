##DESCRIPTION
##  Algebra problem: exponential model of population growth
##ENDDESCRIPTION

## DBsubject(Algebra)
## DBchapter(Exponential and logarithmic expressions and functions)
## DBsection(Exponential and logarithmic equations)
## Institution(CUNY City Tech)
## Author(K. Andrew Parker)
## Level(5)
## Language(en)
## MO(1)
## KEYWORDS('algebra', 'exponential', 'exponential equation', 'logarithms', 'logarithm', 'exponential growth population', 'population growth')


########################################################################

DOCUMENT();      

loadMacros(
   "PGstandard.pl",     # Standard macros for PG language
   "MathObjects.pl",
   "PGML.pl",
   "answerHints.pl"
);

# Print problem number and point value (weight) for the problem
TEXT(beginproblem());

# Show which answers are correct and which ones are incorrect
$showPartialCorrectAnswers = 1;

##############################################################
#
#  Setup
#
#
Context("Numeric");
Context()->variables->are(t=>['Real',limits=>[-1,1]]);
Context()->flags->set(tolerance=>0.0001, tolType=>'relative');

# This problem is coded in such a way as to permit 
# the use of radioactive isotopes that have half-lives
# with other units - though some logic will need to be
# added to the computation of the formula and results
# if units other than years are used...

%data = (
    'Iron-55' => {
        time => 2.737,
        units => "years",
    },
    'Cobalt-60' => {
        time => 5.2714,
        units => "years",
    },
    'Hydrogen-3' => {
        time => 12.32,
        units => "years",
    },
    'Strontium-90' => {
        time => 28.79,
        units => "years",
    },
    'Caesium-137' => {
        time => 30.17,
        units => "years",
    },
    'Uranium-232' => {
        time => 68.9,
        units => "years",
    },
    'Plutonium-238' => {
        time => 87.7,
        units => "years",
    },
    'Americium-241' => {
        time => 432.2,
        units => "years",
    },

);

@isotopes = ('Iron-55','Cobalt-60','Hydrogen-3','Strontium-90','Caesium-137','Uranium-232','Plutonium-238','Americium-241');

#randomly choose an isotope
$i = random(0,7,1);
$atomicName = $isotopes[$i];

# 1/2 = e^(r*time)
# ln(.5)/time = r
$r = ln(0.5)/$data{$atomicName}{time};

# randomly select an initial quantity in kg
$init = random(10,50,5);

$f = Formula("$init e^($r t)");

# how long until $rem kg remains
# make sure we don't accidentally give conditions = half-life

do { $rem = random(2,7,1); } until ( $init != 10 || $rem != 5 );

# ln(init/rem)/$r

$duration = Real(ln($rem/$init)/$r);

sub CloseHint {
  AnswerHints(
    sub {
      my ($correct,$student,$ans) = @_;
      return abs($student-$correct)/abs($correct) < 0.1;
    } => "You're close, try again without rounding 'r' so much."
  );
};
    

##############################################################
#
#  Text
#
#

BEGIN_PGML

Research shows that the radioactive isotope [$atomicName] has a half-life of [$data{$atomicName}{time}] [$data{$atomicName}{units}]

Use the following to construct a function that will model the amount of [$atomicName] remaining after [`t`] years, from an initial amount of [$init] kg.

>> [`` Q(t) = P e^{r t} ``] <<  

Where [` Q(t) `] describes the amount of [$atomicName] remaining after [` t `] years from an initial quantity of [`P`] kg.

1. [`Q(t) = `] [____________]{$f->cmp()}

1. How long (in years) will it take for the amount of [$atomicName] remaining to reach [$rem] kg? [____________]{$duration->cmp()->withPostFilter(CloseHint)}

END_PGML

BEGIN_PGML_HINT
  
**Hints:**

* How long will it take for there to be half of the initial [$init] kg of [$atomicName] remaining? Can you use the model to determine an appropriate value for [`r`]?

END_PGML_HINT


COMMENT("Funded by US DoE Title V: Opening Gateways grant.");

ENDDOCUMENT();        
