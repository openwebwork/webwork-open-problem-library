## DESCRIPTION
## Piedmont College
## MATH 1113 Online Test 3
## Half-life
## ENDDESCRIPTION

## DBsubject(Algebra)
## DBchapter(Exponential and logarithmic expressions and functions)
## DBsection(Applications and models - radioactive decay)
## Institution(Piedmont)
## Author(Doug Torrance)
## Level(2)
## MO(1)
## KEYWORDS('half-life')

DOCUMENT();

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "parserRadioButtons.pl",
);

    
TEXT(beginproblem());

# selections from
# https://en.wikipedia.org/wiki/List_of_radioactive_nuclides_by_half-life
@nuclide = ('flerovium-289', 'hassium-269', 'seaborgium-266', 'dubnium-262',
    'oxygen-14', 'rutherfordium-261', 'nobelium-253', 'mercury-210',
    'flourine-18', 'mendelevium-257', 'erbium-165', 'sodium-24',
    'fermium-253', 'thulium-167', 'mendelevium-258', 'californium-254',
    'rhodium-101', 'curium-243', 'titanium-44', 'silicon-32');
@half_life = (2.6, 27, 30, 34, 1.177, 1.35, 1.62, 10, 1.8295, 5.52, 10.36,
    14.96, 3, 9.25, 51.5, 60.5, 3.3, 29.1, 63, 170);
@unit = ('seconds', 'seconds', 'seconds', 'seconds', 'minutes', 'minutes',
    'minutes', 'minutes', 'hours', 'hours', 'hours', 'hours', 'days',
    'days', 'days', 'days', 'years', 'years', 'years', 'years');
$i = random(0, 19);

do {
    $frac = random(1, 99);
} while ($frac == 50); # make sure we don't make it too easy!

$ans = Round($half_life[$i] * log($frac / 100) / log(0.5), 2);

# make sure the other possible answers seem halfway reasonable
# for the given half-life
$lower_bound = Round($half_life[$i] * log(0.99) / log(0.5), 2);
$upper_bound = Round($half_life[$i] * log(0.01) / log(0.5), 2);
    
sub add_options {
    my @options = shift;
    while (@options < 4) {
        push(@options, random($lower_bound, $upper_bound, 0.01));
        @options = uniq(@options);
    }
    return num_sort(@options);
}

$buttons = RadioButtons([add_options($ans)], $ans, noindex => 1);

BEGIN_TEXT

The half-life of $nuclide[$i] is $half_life[$i] $unit[$i].
$PAR

Determine how many $unit[$i] it would take for a sample to decay to
$frac$PERCENT of its original amount.
$PAR

\{$buttons->buttons\}

END_TEXT

ANS($buttons->cmp);

ENDDOCUMENT();