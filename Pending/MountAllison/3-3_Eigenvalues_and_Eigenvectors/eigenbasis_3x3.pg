## DESCRIPTION
## Compute a basis for the eigenspace corresponding to some eigenvalue of a 3x3 matrix.
##   All coefficients and eigenvalues will be small real integers. The eigenvalue will have algebraic and geometric multiplicity 2, so 2 basis vectors are expected.
##   There will be a solution (i.e., a basis) with small integer entries.
##   There are infinitely many different solutions (i.e., bases). They are all accepted.
## ENDDESCRIPTION

## DBsubject(Linear algebra)
## DBchapter(Eigenvalues and eigenvectors)
## DBsection(Computing eigenvalues and eigenvectors)
## Date(March 22, 2020)
## Institution(Mount Allison University)
## Author(Nathaniel Johnston)
## MO(1)
## TitleText1('Introduction to Linear and Matrix Algebra')
## AuthorText1('Nathaniel Johnston')
## EditionText1('1')
## Section1('3.3')
## Problem1('')
## KEYWORDS('linear algebra','matrix','eigenvalue','algebraic multiplicity','geometric multiplicity')
## http://www.njohnston.ca/publications/introduction-to-linear-and-matrix-algebra/

##############################################################
#  Initialization
##############################################################

DOCUMENT();      

loadMacros(
    "PGstandard.pl",
    "MathObjects.pl",
    "parserMultiAnswer.pl",
);

TEXT(beginproblem());

##############################################################
#  Problem Setup
##############################################################

Context('Matrix');
do {
    $flg = random(1,5);
    if($flg == 1) {
        $P = Matrix([[2,5,-2],[0,3,-1],[-1,-2,1]]);
        $PI = Matrix([[1,-1,1],[1,0,2],[3,-1,6]]);
    } elsif($flg == 2) {
        $P = Matrix([[3,0,1],[1,1,1],[2,0,1]]);
        $PI = Matrix([[1,0,-1],[1,1,-2],[-2,0,3]]);
    } elsif($flg == 3) {
        $P = Matrix([[1,-2,2],[-2,5,-4],[-1,2,-1]]);
        $PI = Matrix([[3,2,-2],[2,1,0],[1,0,1]]);
    } elsif($flg == 4) {
        $P = Matrix([[1,1,-1],[0,1,-1],[2,0,1]]);
        $PI = Matrix([[1,-1,0],[-2,3,1],[-2,2,1]]);
    } else {
        $P = Matrix([[2,1,1],[3,2,1],[1,1,1]]);
        $PI = Matrix([[1,0,-1],[-2,1,1],[1,-1,1]]);
    }
    
    do {
        $eig1 = random(-4,5);
        $eig2 = random(-4,5);
            
        $D = Matrix([[$eig1,0,0],[0,$eig2,0],[0,0,$eig2]]);
            
        $A = $P * $D * $PI;
    } until ($eig1 != $eig2);
} until ($A->norm_one < 25);

Context("Vector");
Context()->lists->set(Vector => {open => "(", close => ")"});
Context()->parens->set("(" => {type => "Vector", close => ")"});
$v1 = Vector($P->column(2));
$v2 = Vector($P->column(3));

$multians = MultiAnswer($v1,$v2)->with(
    singleResult => 1,
    allowBlankAnswers => 0,
    separator => ', ', 
    tex_separator => ', ', 
    checker => sub {
        my ( $correct, $student, $answerHash ) = @_;
        my ( $s0, $s1 ) = @{$student};
        if((Vector($A * $s0) == $eig2 * $s0) && (Vector($A * $s1) == $eig2 * $s1) && $s0->norm > 0.001 && $s1->norm > 0.001) {
            return 1 - ($s0->isParallel($s1));
        } else {
            return 0;
        }
    }
);

##############################################################
#  Text
##############################################################

Context()->texStrings;
BEGIN_TEXT
Compute a basis of the eigenspace corresponding to the eigenvalue \( $eig2 \) of the matrix \( \displaystyle $A \).
$BR
$BR
\( \Bigg\lbrace \)\{$multians->ans_array(3)\}, \{$multians->ans_array(3)\}\( \Bigg\rbrace \).
END_TEXT
Context()->normalStrings;

##############################################################
#  Answers and Solutions
##############################################################

$showPartialCorrectAnswers = 1;

ANS( $multians->cmp() );

ENDDOCUMENT();
