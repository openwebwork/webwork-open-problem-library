##DESCRIPTION
##  Algebra problem: Add rational expressions
##ENDDESCRIPTION

##KEYWORDS('algebra', 'algebra, operations with rational expressions', 'algebra, simplifying rational expression', 'rational expressions', 'rational expression')
## DBsubject(Algebra)
## DBchapter(Operations on polynomial and rational expressions)
## DBsection(Rational expressions: add, subtract)
## Level(2)
## Author(K. Andrew Parker)
## Institution(CUNY City Tech)
## Language(en)

########################################################################

DOCUMENT();      

loadMacros(
   "PGstandard.pl",     # Standard macros for PG language
   "MathObjects.pl",
   "contextLimitedPowers.pl"
);

# Print problem number and point value (weight) for the problem
TEXT(beginproblem());

# Show which answers are correct and which ones are incorrect
$showPartialCorrectAnswers = 1;

$rationalChecker = sub {
     my ($correct,$student,$ansHash) = @_; 
     $studentRaw = $student;
     Value->Error("Your answer doesn't contain any variables. <BR> Perhaps you oversimplified?") unless $student->class eq "Formula";
     if ($student->{tree}->class eq "UOP" && $student->{tree}{uop} eq 'u-') {
          $student = Formula($student->{tree}->{op});
     }
     Value->Error("Check the answer preview box. Maybe you forgot parenthesis around your numerator or denominator?") unless $student->{tree}->class eq "BOP" && $student->{tree}{bop} =~ m!/!;
     return 0 unless $correct == $studentRaw;
     my $cnum = Formula($correct->{tree}{lop});
     my $snum = Formula($student->{tree}{lop});
     my $cden = Formula($correct->{tree}{rop});
     my $sden = Formula($student->{tree}{rop});
     Value->Error("Your answer can be reduced further") unless (abs($cnum) == abs($snum) && abs($cden) == abs($sden));
     return 1;
};

##############################################################
#
#  Setup
#
#
Context("Numeric");
LimitedPowers::OnlyPositiveIntegers();

$coeff = random(2,5,1);

$dr1 = random(3,9,1);
$dr2 = random(-9,-3,1);
$dA = $coeff;
$dB = $coeff * ( $dr1 + $dr2 );
$dC = $coeff * $dr1 * $dr2;
$denom = Formula("$dA*x^2+$dB*x+$dC")->reduce;

$drSum = $dr1 + $dr2;  #for solution
$drProd = $dr1 * $dr2;   #for solution

$n1 = Formula("$coeff * x");
$n2 = $coeff * $dr1;

$frac1 = Formula("$n1/$denom");
$frac2 = Formula("$n2/$denom");

$ans = Formula("1/(x+$dr2)")->reduce;

##############################################################
#
#  Text
#
#

Context()->texStrings;
BEGIN_TEXT

$PAR
Combine and simplify if possible.
$PAR
\[ $frac1 + $frac2 \]
$PAR
\{ ans_rule(20) \}

END_TEXT
Context()->normalStrings;

##############################################################
#
#  Answers
#
#

ANS($ans->cmp(checker=>$rationalChecker));

Context()->texStrings;
BEGIN_HINT

These two fractions already have a common denominator - so you may simply add the numerators together. $BR
Remember, you may only combine $BITALIC like terms! $EITALIC
$PAR
Also, don't forget to reduce your final answer, if possible.

END_HINT
BEGIN_SOLUTION

The denominators are already the same, so we may add the fractions: $BR
\( \displaystyle $frac1 + $frac2 = \frac{$n1 + $n2}{$denom} \)
$PAR
Now, can our result be simplified? $BR
Factor the numerator: $BR 
\( $n1 + $n2 = $coeff( x + $dr1 ) \) $BR
Factor the denominator: $BR
\( $denom = $coeff( x^2 + $drSum x + $drProd ) \) $BR
\( $coeff( x^2 + $drSum x + $drProd ) = $coeff( x + $dr1 )( x + $dr2 )\)
$PAR
Now rewrite our answer: $BR
\( \displaystyle \frac{$coeff( x + $dr1 )}{$coeff( x + $dr1 )( x + $dr2 )} = \left(\frac{$coeff}{$coeff}\right) \left(\frac{x + $dr1}{x + $dr1}\right) \left(\frac{1}{x+$dr2}\right) = (1)(1)\left($ans\right) = $ans \)
END_SOLUTION
Context()->normalStrings;

COMMENT("Funded by US DoE Title V: Opening Gateways grant.");

ENDDOCUMENT();        