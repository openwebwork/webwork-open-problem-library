## DESCRIPTION
## Linear Algebra
## ENDDESCRIPTION


## DBsubject(Linear algebra)
## DBchapter(Euclidean spaces)
## DBsection(Subspaces)
## Date(July 2013)
## Institution(Hope College)
## Author(Paul Pearson)
## KEYWORDS('linear algebra','vector space','subspaces')

DOCUMENT();
loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "unionLists.pl",
  "unionTables.pl",
  "PGgraphmacros.pl",
  "parserPopUp.pl",
  "PGcourse.pl"
);

$showPartialCorrectAnswers = 0;
$refreshCachedImages = 1;
TEXT(beginproblem()); 

Context('Vector2D')->strings->add( 'CLOSED' => {caseSensitive=>0} );

do {
$a = random(-1,1,2) * random(2,4,1);
$b = random(-1,1,2) * random(2,4,1);
$c = $a * $b;
} until gcd($a,$b) == 1;

#
#  Graph canvas
#
$gr = init_graph(-5,-5,5,5,axes=>[0,0],pixels=>[200,200]);

#
#  Graph labels
#
$gr->lb('reset');
$gr->lb( new Label(4.5,0.35,'x','black','center','middle'));
$gr->lb( new Label(0.35,4.5,'y','black','center','middle'));


#
#  Define new graph colors
#
$gr->new_color("lightblue", 214,230,244); # RGB
$gr->new_color("darkblue",  100,100,255);
$gr->new_color("lightgreen", 187, 255, 153); 
$gr->new_color("darkgreen",    0, 208, 0);
$gr->new_color("lightred",  245,234,229); # light red-purple
$gr->new_color("darkred",   159, 64, 16); # red-brown


#
#  Choose colors
#
$light = "lightgreen";
$dark = "darkgreen";

#
#  Graph the function and the filled region
#
add_functions($gr, "(-1*$a/$b)*x+$a for x in <-5,5> using color:red and weight:2");

$zero = PopUp(['choose','H is empty','H is nonempty'],'H is nonempty');
$subspace = PopUp(['choose','H is a subspace of V','H is not a subspace of V'],'H is not a subspace of V');


$list1 = List(Vector($b,0),Vector(0,$a));
$list2 = List(Real(2),Vector($b,0));

Context()->texStrings;
BEGIN_TEXT
\{
ColumnTable(
"Let \( V = \mathbb{R}^2 \) and let \( H \) be the subset of \( V \) 
of all points on the line \( $a x + $b y = $c \).
Is \( H \) a subspace of the vector space \( V \)?
$BR
$BR".BeginList('OL')."
$ITEM Is \( H \) nonempty? $BR".$zero->menu()."
$ITEMSEP
$ITEM Is \( H \) closed under addition?  If it is, enter ${BITALIC}CLOSED${EITALIC}.
If it is not, enter two vectors in \( H \) whose sum is not in \( H \), 
using a comma separated list and syntax such as \( \verb+<1,2>, <3,4>+ \).
$BR".$list1->ans_rule(30)."
$ITEMSEP
$ITEM Is \( H \) closed under scalar multiplication?  If it is, enter ${BITALIC}CLOSED${EITALIC}.
If it is not, enter a scalar in \( \mathbb{R} \) and a vector in \( H \) whose product is not in \( H \),
using a comma separated list and syntax such as \( \verb+2, <3,4>+ \).
$BR".$list2->ans_rule(30)."
$ITEMSEP
$ITEM Is \( H \) a subspace of the vector space \( V \)? 
You should be able to justify your answer by writing a complete, coherent, and detailed proof 
based on your answers to parts 1-3.
$BR".$subspace->menu().EndList('OL')
,
image( insertGraph($gr),height=>200,width=>200,tex_size=>500 )
,
indent => 0, separation => 30, valign => "TOP"
);
\}
END_TEXT
Context()->normalStrings;

install_problem_grader(~~&std_problem_grader);

sub mycheck_add {

  my ($correct, $student, $ansHash, $value) = @_;
  my @s = @{$student};
  my $score = 0;
  my @errors = ();

  return 0 if !defined($s[1]);

  if ( ($s[0]->class eq 'Vector') and ($s[1]->class eq 'Vector') ) {
    my ($ux,$uy) = $s[0]->value;
    my ($vx,$vy) = $s[1]->value;
    my ($wx,$wy) = ($ux + $vx, $uy + $vy);
    my $z = Real(0);

    my $u_inside = 0;
    my $v_inside = 0;
    my $w_outside = 0;
    $u_inside = 1 if ( $a * $ux + $b * $uy == $c );
    $v_inside = 1 if ( $a * $vx + $b * $vy == $c );
    $w_outside = 1 if ( $a * $wx + $b * $wy != $c );
    if ( $u_inside and $v_inside and $w_outside ) { 
      $score = 2; 
    } 
  } 

  return ($score,@errors);

}


sub mycheck_mult {

  my ($correct, $student, $ansHash, $value) = @_;
  my @s = @{$student};
  my $score = 0;
  my @errors = ();

  return 0 if !defined($s[1]);

  my $k = Real(0);  # the student's scalar will be assigned to $c later
  my $v = Vector(0,0); # the student's vector will be assigned to $v later
  my $z = Real(0);
  if ( ($s[0]->class eq 'Real') and ($s[1]->class eq 'Vector') ) { $k = $s[0]; $v = $s[1]; }
  if ( ($s[1]->class eq 'Real') and ($s[0]->class eq 'Vector') ) { $k = $s[1]; $v = $s[0]; }
  my ($vx,$vy) = $v->value;

  if ( ($a * $vx + $b * $vy == $c) and ($a * $k * $vx + $b * $k * $vy != $c) ) { $score = 2; } 

  return ($score,@errors);

}


ANS($zero->cmp);
ANS($list1->cmp(list_checker=>~~&mycheck_add));
ANS($list2->cmp(list_checker=>~~&mycheck_mult));
ANS($subspace->cmp);
COMMENT('MathObject version.  Randomly chooses the x- and y-intercepts of the line.  The line never goes through the origin.');
ENDDOCUMENT();
