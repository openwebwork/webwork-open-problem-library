# Plot three lines that each have the given slope.
# Author: Alex Jordan

###################################################################################
# Setup and Macros
###################################################################################
DOCUMENT();

loadMacros(
	"PGstandard.pl",
	"MathObjects.pl",
	"PGML.pl",
	"contextFraction.pl",
	"parserGraphTool.pl",
	"PGcourse.pl",
);

###################################################################################
# Variables and Formulas
###################################################################################
TEXT(beginproblem());

($rise, $run) = random_coprime([1..5],[2..5]);

Context("Numeric");
@A = ();
for my $x (-8..(8-$run)) {
	for my $y (-8..(-9+$rise)) {
		push(@A, Point($x,$y));
	}
}
for my $x (-8..(-9+$run)) {
	for my $y ((-8+$rise)..(8-$rise)) {
		push(@A, Point($x,$y));
	}
}
%B = map{$_ => $_+Point($run,$rise)}(@A);
@gtlines = map{'{line,solid,' . $_ . ',' . $B{$_} .'}'} (@A);


Context("Fraction");
$m = Fraction($rise,$run);

$gt = GraphTool(join(',',@gtlines))->with(
	availableTools => ["LineTool"],
	bBox => [-8, 8, 8, -8],
	cmpOptions => {
		list_checker => sub {
			my ($correct, $student, $ans, $value) = @_;
			return 0 if $ans->{isPreview};

			my @errors;
			my $count = 0;
			my $extra = 0;
			my $distinct = 0;

                        STUDENTS:
                        for my $j (@$student) {
				my $nth = Value::List->NameForNumber(++$count);

				my ($p1x, $p1y) = $j->extract(3)->value;
				my ($p2x, $p2y) = $j->extract(4)->value;
				if ($p1x == $p2x) {
					push(@errors, "The $nth plotted line is a vertical line, and therefore does not have slope $m.");
					$extra++;
					next STUDENTS;
				}

				my $context = Context();
				Context("Fraction");
				my $slope = Fraction($p2y - $p1y, $p2x - $p1x);
				Context($context);
				if ($slope != $m) {
					push(@errors, "The $nth plotted line has slope $slope, not slope $m.");
					$extra++;
					next STUDENTS;
				}

                                for my $i (0..($count-2)) {
					my ($x,$y) = $student->[$i]->extract(3)->value;
					next STUDENTS if ($y == $m*($x - $p1x) + $p1y);
                                }
				$distinct++;
                        }
                        push(@errors, "You've only plotted $distinct correct line".(($distinct > 1)?'s':'').".") if ($distinct > 0 && $distinct < 3);
                        return (scalar(@{$correct})*min($distinct,3)/3/(($extra)?2:1), @errors);
		}
	}
);

###################################################################################
# Main Text
###################################################################################

BEGIN_PGML
Plot at least three lines that each have slope [`[$m]`].

[_]{$gt}
END_PGML

ENDDOCUMENT();


