# Plot four points on the [-7,7]x[-7,7] plane.
# One in each quadrant, except one moved onto an axis.
# Author: Alex Jordan

###################################################################################
# Setup and Macros
###################################################################################
DOCUMENT();

loadMacros(
	"PGstandard.pl",
	"MathObjects.pl",
	"PGML.pl",
	"PGchoicemacros.pl",
	"parserGraphTool.pl",
	"PGcourse.pl",
);

###################################################################################
# Variables and Formulas
###################################################################################
TEXT(beginproblem());

Context("Numeric");

@xy = (0..7)[NchooseK(8,8)];
$point[0] = Point($xy[0],$xy[1]);
$point[1] = Point(-$xy[2],$xy[3]);
$point[2] = Point($xy[4],-$xy[5]);
$point[3] = Point(-$xy[6],-$xy[7]);
@point = (@point)[NchooseK(4,4)];

$gt = GraphTool("{point,$point[0]},{point,$point[1]},{point,$point[2]},{point,$point[3]}")->with(
	availableTools => ["PointTool"],
	showCoordinateHints => 0,
	bBox => [-8, 8, 8, -8],
	cmpOptions => {
		list_checker => sub {
			my ($correct, $student, $ans, $value) = @_;
			my @score = (0,0,0,0);
			my @errors;
			my $count = 0;
			STUDENTS:
			for my $j (@$student) {
				my $nth = Value::List->NameForNumber(++$count);
				for my $i (0..3) {
					if ($j->extract(1) eq $correct->[$i]->extract(1) && $j->extract(2) eq $correct->[$i]->extract(2)) {
						$score[$i] += 1;
						next STUDENTS;
					}
				}
				push(@errors, "The $nth plotted point is incorrect.");
			}
			my $numerator = ($score[0] ? 1 : 0) + ($score[1] ? 1 : 0) + ($score[2] ? 1 : 0) + ($score[3] ? 1 : 0);
			push(@errors, "You have only plotted $numerator of the given points.") if ($numerator > 0 && $numerator < 4);
			my $denominator = 4 + $count - ($score[0] + $score[1] + $score[2] + $score[3]);
			return ($numerator/$denominator*$count, @errors);
		}
	}
);

###################################################################################
# Main Text
###################################################################################

BEGIN_PGML
Graph the points [`[$point[0]]`], [`[$point[1]]`], [`[$point[2]]`], and [`[$point[3]]`].

[_]{$gt}
END_PGML

ENDDOCUMENT();


