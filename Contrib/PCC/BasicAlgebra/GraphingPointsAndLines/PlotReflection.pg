# Plot a point where one coordinate is negated from a given plotted point.
# Author: Alex Jordan

###################################################################################
# Setup and Macros
###################################################################################
DOCUMENT();

loadMacros(
	"PGstandard.pl",
	"MathObjects.pl",
	"PGML.pl",
	"PGchoicemacros.pl",
	"parserGraphTool.pl",
	"PGcourse.pl",
);

###################################################################################
# Variables and Formulas
###################################################################################
TEXT(beginproblem());

Context("Numeric");

@xy = (1..7)[NchooseK(7,2)];
$point = Point(@xy);
$reflection = list_random(
	['first',Point(-$xy[0],$xy[1])],
	['second',Point($xy[0],-$xy[1])]
);

$gt = GraphTool("{point,$reflection->[1]}")->with(
	availableTools => ["PointTool"],
	showCoordinateHints => 0,
	bBox => [-8, 8, 8, -8],
	staticObjects => [ "{point,$point}"],
	cmpOptions => {
		list_checker => sub {
			my ($correct, $student, $ans, $value) = @_;
			my $score = 0;
			my @errors;
			my $count = 0;
			for my $j (@$student) {
				my $nth = Value::List->NameForNumber(++$count);
				if ($j->extract(1) eq $correct->[0]->extract(1) && $j->extract(2) eq $correct->[0]->extract(2)) {
					$score += 1;
					next;
				}
				push(@errors, "The $nth plotted point is incorrect.");
			}
			return ($score, @errors);
		}
	}
);

###################################################################################
# Main Text
###################################################################################

BEGIN_PGML
A point is plotted. Graph the point that comes from negating the [@$reflection->[0]@] coordinate.

[_]{$gt}
END_PGML

ENDDOCUMENT();


