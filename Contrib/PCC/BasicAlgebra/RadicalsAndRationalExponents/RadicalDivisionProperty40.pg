# WeBWorK problem written by Carl Yao
# Portland Community College
#
# Simplify radical using the multiplication and division properties.
# Problem is like sqrt(8/7)*sqrt(3/7). Answer is a fraction with a radical
# in numerator. The radical in the solution can be simplified.
#
# Last edited: Carl Yao 8/31/13,
#
# ENDDESCRIPTION

## DBsubject('Algebra')
## DBchapter('Basic Algebra')
## DBsection('Exponents and Radicals')
## KEYWORDS('simplify','radical','square root','fraction','multiply')
## DBCCSS('8.EE')
## TitleText1('')
## EditionText1('')
## AuthorText1('')
## Section1('')
## Problem1('')
## Author('Alex Jordan, Carl Yao, Chris Hughes')
## Institution('PCC')

######################################

DOCUMENT();

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "PGML.pl",
  "contextFraction.pl",
  "contextLimitedRadical.pl",
  "PGcourse.pl",
);

######################################

TEXT(beginproblem());
Context("Numeric");

do {
   $out = list_random(2,3,4,5,6);
   $in = list_random(2,3,5,7);
   $rad1 = $in*$out;
   $rad2 = $out;
} until (int(sqrt($rad1))!=sqrt($rad1));
$den1 = list_random(11,13,17,19);
$den2 = $den1;

$denSquared = $den1**2;
$radCombined = $rad1*$rad2;

Context("LimitedRadical");
$sqrt1 = Formula("sqrt($rad1/$den1)");
$sqrt2 = Formula("sqrt($rad2/$den2)");
$ans = Formula("$out*sqrt($in)/$den1");

##########################################################

#Input is a prime. Output is the next prime number.
sub nextPrime {
   my $p = shift;
   while (1) {
      $p++;
      if ($p%2==0) {next;}
      $isPrime = 1;
      for (my $i=3; $i<=sqrt($p); $i=$i+2) {
         if ($p%$i==0) {
            $isPrime=0;
            last;
         }
      }
      if ($isPrime==1) {return $p;}
   }
}

# Input is a number. Output is the prime factorization of the number
# saved in an array, returned as a reference to the array.
sub primeFactor {
   my $n = shift;
   @factors = ();
   my $prime=2;
   while (1) {
      while ($n%$prime==0) {
         $factors[scalar @factors] = $prime;
         $n=$n/$prime;
      }
      if ($n==1) {return ~~@factors;}
      $prime = &nextPrime($prime);
   }
}

# Input 1 is a reference to an array with a number's prime factorization.
# Output is all prime factors connected by ' \cdot '.
sub line1 {
   my $ra_factors = shift;
   $line1Output = "";
   foreach $item ( @$ra_factors ) {$line1Output .= "$item".' \cdot ';}
   $line1Output =~ s/ \cdot $//;
   return $line1Output;
}

# Input is the output of &line1() and the degree of the radical. 
# $returnLine2 is the next step of simplifying the radical. 
# For example, if input is "2 \cdot 2 \cdot 2 \cdot 3 \cdot 5", 
# $degree=3, (meaning cube root), 
# $returnLine2 is 2 sqrt[3]{3 \cdot 5}.
# $returnLine3 is the final step of simplifying the radical.
# In the above example, $returnLine3 is 2 sqrt[3]{15}.
sub line2 {
   my $line = shift;
   my $degree = shift;
   my $line2Output = "";
   my @facs = split(/ \cdot /, $line);
   my $size = 0+@facs;
   my $i=0;
   $outsideString = "";
   $insideString = "";
   $outsideNumber = 1;
   $insideNumber = 1;
   while ($i<$size) {
      my $item = $facs[$i];
      my $out = 1;
      for (my $j=$i; $j<$i+$degree; $j++) {
         if ($facs[$j]!=$item) {$out = 0;}
      }
      if ($out==0) {
         $insideNumber *= $facs[$i];
         $insideString .= "$facs[$i]".' \cdot ';
         $i++;
      }
      else {
         $outsideNumber *= $facs[$i];
         $outsideString .= "$facs[$i]".' \cdot ';
         $i=$i+$degree;
      }
   }
   $outsideString =~ s/ \cdot $//;
   $insideString =~ s/ \cdot $//;

   $returnLine2 = "";
   if ($insideString eq "") {$returnLine2 = $outsideString;}
   elsif ($degree==2) {$returnLine2 = 
        $outsideString.' \sqrt{'."$insideString".'}';}
   else {$returnLine2 = 
        $outsideString.' \sqrt['."$degree".']{'."$insideString".'}';}

   $returnLine3 = "";
   if ($returnLine2 !~ /\cdot/) {$returnLine3 = "";}
   elsif ($degree==2) {$returnLine3 =
        $outsideNumber.' \sqrt{'."$insideNumber".'}';}
   else {$returnLine3 = 
        $outsideNumber.' \sqrt['."$degree".']{'."$insideNumber".'}';}

   return ($returnLine2,$returnLine3);
}

#Output of simplifying $radCombined
$line1Center = &line1(&primeFactor($radCombined));
$output11 = '&= \sqrt{'.$line1Center.'} ';
($line2Center, $line3Center) = &line2($line1Center,2);
$output12 = '&='.$line2Center;
$output13 = "&";
if ($line3Center ne "") {$output13 = '&='.$line3Center;}


##########################################

BEGIN_PGML

Simplify the expression [`` [$sqrt1] \cdot [$sqrt2]  ``].

Use *sqrt()* for the square root symbol. For example: [`\sqrt{2} =`]  *sqrt(2)*.

    [`` [$sqrt1] \cdot [$sqrt2] =``] [_____________]{$ans}

END_PGML

############################################################

BEGIN_PGML_SOLUTION

First, we use the multiplication property of radicals:

    [`` \sqrt{a} \cdot \sqrt{b} = \sqrt{ab} ``]

We have:

    [`` 
\begin{aligned}[t]
   [$sqrt1] \cdot [$sqrt2]
       &= \sqrt{ \frac{[$rad1]}{[$den1]} \cdot \frac{[$rad2]}{[$den2]} } \\
       &= \sqrt{ \frac{[$radCombined]}{[$denSquared]} }
\end{aligned}
    ``]

Next, we will use the division property of radicals:

    [`` \sqrt{\frac{a}{b}} = \frac{\sqrt{a}}{\sqrt{b}} ``]

We have:

    [`` 
\begin{aligned}[t]
   \sqrt{ \frac{[$radCombined]}{[$denSquared]}}
      &= \frac{\sqrt{[$radCombined]}} {\sqrt{[$denSquared]}} \\
      &= \frac{\sqrt{[$radCombined]}} {[$den1]} \\
\end{aligned}
    ``]

We must continue by simplifying the radical [` \sqrt{[$radCombined]} `]:

    [``
\begin{aligned}[t]
   \sqrt{[$radCombined]} [$output11] \\
            [$output12] \\
            [$output13]
\end{aligned}
    ``]

The final answer is:

    [`` \frac{[$out]\sqrt{[$in]}} {[$den1]} ``]

END_PGML_SOLUTION

######################################



ENDDOCUMENT();
