##DESCRIPTION
## Integration of sqrt(a^2-x^2) using trig substitution. The first part of the scaffolding 
## sets up the trig substitution using the sine function. If the student chooses to use the
## cosine function, a message is given to use sine instead. A student's correct answer for the trig
## integral after the substitution is checked to see if it continues to use the square root.
## If so, the answer is marked as incorrect and a message is given to simplify and not use square root.

## The second part presents the student with the correct antiderivative of cos^2(t) so that
## the student is only asked to do the substitution back in terms of x.

##ENDDESCRIPTION

## DBsubject(Calculus - single variable)
## DBchapter(Techniques of integration)
## DBsection(Trigonometric substitution)
## Institution(Agnes Scott College)
## Author(Larry Riddle)
## Level(2)
## MO(1)
## TitleText1('APEX Calculus')
## AuthorText1('Hartman')
## EditionText1('4.0')
## Section1('6.4')
## Problem1('8')


DOCUMENT();

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "PGML.pl",
  "scaffold.pl",
  "PGtikz.pl",
  "parserFormulaUpToConstant.pl",
  "PGcourse.pl"
);

$showHint = 0;

TEXT(beginproblem());

Context("Numeric");
Context()->variables->add(t => "Real");
Context() -> variables -> set(t =>{limits => [-pi/2,pi/2]},x=>{limits=>[0,2]});

$b = random(2,12);
$f = Formula("sqrt($b^2-x^2)");

# Trig substitutiion
$ans1a = Formula("$b*sin(t)");
$ans1b = Formula("$b*cos(t)");
$ans2 = Formula("$b*cos(t)");          # derivative
$ans3 = Formula("$b^2*cos(t)^2");      # trig integrand

# Substituting back in terms of x
$ans4 = Formula("asin(x/$b)");         # t
$ans5 = Formula("x/$b");               # sin(t)
$ans6 = Formula("sqrt($b^2-x^2)/$b");  # cos(t)
$ans7 = Formula("x*sqrt($b^2-x^2)/2 + $b^2*asin(x/$b)/2");

$Subchecker = sub {
    my ($c, $s, $ansHash) = @_;
    if ($s == $ans1b) {
         Value->Error("The cosine function will work, but the sine function is usually used for the trig substitution. Use that here instead of the cosine function, with adjustment to your other answers.");
    } else {
         return ($s == $c) ? 1 : 0
    }
};

$IntChecker = sub {
    my ($c, $s, $ansHash) = @_;
    my $s_str = $s->string;
    my $n1 = index($s_str,'sqrt');
    my $n2 = index($s_str,'^(1/2)');
    my $n3 = index($s_str,'^0.5');
    if ( $n1 + $n2 + $n3 > -3 && $c == $s ) {
        Value->Error("The substitution was done correctly, but now simplify your answer to avoid the square root")
    } else {
        return ($c == $s) ? 1 : 0
    }
};


BEGIN_PGML

The goal of this problem is to evaluate [``\int [$f]\;dx``] using trig substitution.

Click on Part 1 to open the section. Enter your answers for the three steps in Part 1 to set up the trig substitution, then click on Submit Answers. Once your answers are all correct, you will be able to open Part 2 to take the value of the trig integral and substitute back in terms of [`x`].

END_PGML

Scaffold::Begin(open_first_section => 0);

Section::Begin("Part 1: The Trig Substitution Setup",
is_open => correct_or_first_incorrect);

BEGIN_PGML

Give an appropriate trig substitution in the form [`x = f(t)`] (where [`t`] represents [`\theta`]).

[`x = \;`][__]{$ans1a->cmp(checker=>$Subchecker)}{10}

[`dx = \;`][__]{$ans2}{15}[`\;dt`]

Now substitute and simplify to get a trig integral in terms of [`t`]. The trig integral should no longer involve a square root.

[``\int\;[$f]\;dx = \int``][__]{$ans3->cmp(checker=>$IntChecker,bypass_equivalence_test => 1)}{50}[`\;dt`]

END_PGML

$b2 = $b*$b;
$b2d2 = $b2/2;
$image = createTikZImage();
    $image->BEGIN_TIKZ
    \draw[-] (0,0) -- (4,0) node[midway, below] {\( ?? \)};
    \draw[-] (4,0) -- (4,2) node[midway, right] {\( x \)};
    \draw[-] (4,2) -- (0,0) node[midway, above] {\( $b \)};
    \node at (0.7,.2) {\(\ t \)};
    END_TIKZ

BEGIN_PGML_SOLUTION

The choice of [`x = [$b]\sin(t)`] is the usual trig substitution for integrands of the form [`\sqrt{[$b2]-x^2}`]. Then [`dx = [$b]\cos(t)dt`]. Since [`t = \sin^{-1}(x/[$b])`] for [`-\pi/2 \le t \le \pi/2`] and [`\cos(t)\ge0`] on this interval, substituting for [`x`] and [`dx`] in the original integral gives

[``
\begin{aligned}
\int\;[$f]\;dx &= \int \sqrt{[$b2]-[$b2]\sin^2(t)}\cdot([$b]\cos(t)dt) = \int [$b]\sqrt{1-\sin^2(t)}\cdot[$b]\cos(t)dt \\ \\
&= \int [$b2]\sqrt{\cos^2(t)}\cdot\cos(t)dt= \int [$b2]\cos(t)\cdot\cos(t)dt = \int [$b2]\cos^2(t)dt.
\end{aligned}
``]

END_PGML_SOLUTION

Section::End();

Section::Begin("Part 2: Evaluating the Trig Integral",
can_open => when_previous_correct,
is_open => correct_or_first_incorrect);

BEGIN_PGML

From previous integration techniques we know that [``\int \cos^2(t)\;dt = \frac{t}{2}+\frac{\sin(t)\cos(t)}{2}``].

Use this to evaluate the trig integral from Part 1 and then substitute back to get the final result in terms of [`x`].

To substitute back in terms of [`x`], you would use

[`\quad\quad t =\; `][_]{$ans4}{15}

[`\sin(t) =\; `][_]{$ans5}{15}

[`\cos(t) =\; `][_]{$ans6}{15}

The final result would then be

[``\int [$f]\;dx =\; ``][_]{$ans7->cmp(upToConstant=>1)}{50}[`\;+\;C`]

Note: You can enter [`\arcsin(x)`] or [`\sin^{-1}(x)`] for the inverse sine function.

END_PGML

BEGIN_PGML_HINT
Complete the following reference triangle where [`x = [$b]\sin(t)`] to help find the value of [`\cos(t)`].

[@ image($image, width => 200, tex_size => 250) @]*
END_PGML_HINT


BEGIN_PGML_SOLUTION

[``
\begin{aligned}
\int\;[$f]\;dx &= \int [$b2]\cos^2(t)dt = [$b2] \int \frac{1+\cos(2t)}{2}dt \\ \\
&= [$b2]\left(\frac{t}{2} + \frac{1}{4}\sin(2t)\right) + C = [$b2]\left(\frac{t}{2}+\frac{\sin(t)\cos(t)}{2}\right) + C \\ \\
&=[$b2d2](t+\sin(t)\cos(t)) + C
\end{aligned}
``]

Now [`x = [$b]\sin(t)`] means that [``\sin(t) = \frac{x}{[$b]}``] and so [``t = \sin^{-1}\left(\frac{x}{[$b]}\right)``]. 

In addition,

[``\cos(t)=\sqrt{1-\sin^2(t)} = \sqrt{1-\frac{x^2}{[$b2]}} = \frac{\sqrt{[$b2]-x^2}}{[$b]} ``].

Therefore

[``
\begin{aligned}
\int\;[$f]\;dx &= [$b2d2](t+\sin(t)\cos(t)) + C = [$b2d2]\sin^{-1}\left(\frac{x}{[$b]}\right) +  
[$b2d2]\cdot \frac{x}{[$b]}\cdot \frac{\sqrt{[$b2]-x^2}}{[$b]} + C \\ \\
&= [$b2d2]\sin^{-1}\left(\frac{x}{[$b]}\right) + \frac{1}{2}x\sqrt{[$b2]-x^2}+C.
\end{aligned}
``]

END_PGML_SOLUTION

Section::End();
Scaffold::End();


COMMENT('Randomization provides 11 different possible versions of this question.');

ENDDOCUMENT();