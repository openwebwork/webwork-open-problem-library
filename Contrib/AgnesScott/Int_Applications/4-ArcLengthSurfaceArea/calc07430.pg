##DESCRIPTION
## Surface Area
## Student must setup the integral, entering limits and integrand,
## Limits weighted 10%, integrand 50%, answer 40%
## These percentages can be adjusted if desired.
##ENDDESCRIPTION

## Answer boxes for limits of integration Coding in PG - Nathan Wallach (CSS based formatting work and more)
## Answer boxes for limits of integration Coding in PGML - Glenn Rice
## https://webwork.maa.org/moodle/mod/forum/discuss.php?d=4767#p14157
## DBsubject(Calculus - single variable)
## DBchapter(Applications of integration)
## DBsection(Surfaces of revolution)
## Institution(Agnes Scott College)
## Author(Larry Riddle)
## Level(2)
## MO(1)
## TitleText1('APEX Calculus')
## AuthorText1('Hartman')
## EditionText1('4.0')
## Section1('7.4')
## Problem1('30')

DOCUMENT();

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "PGML.pl",
  "weightedGrader.pl",
  "answerHints.pl",
  "PGcourse.pl"
);

install_weighted_grader();

TEXT(beginproblem());

$showPartialCorrectAnswers = 1;

Context("Numeric");

$a = random(2,5);
do {$b = random(1,9);} until ($b != $a);
$xlow = Real(0);
$xhigh = Real(random(1,3));
$f = Formula("$a*x^2+$b");
$Df = $f->D;
$Df = Compute("$Df");
$integrand = Formula("2*pi*x*sqrt(1+($Df)^2)");
$ans = Real("2*pi*((4*($a)^2*($xhigh)^2 + 1)^(3/2) - 1)/(12*($a)^2)");


# Code to format the answer boxes for integration limits
TEXT( MODES(
  HTML=>"
    <style>
      .lowerIntegrationBoundOfPair input[type=text].codeshard {
        padding:1px;
        font-size:11pt;
        height:20px !important;
      }
      .upperIntegrationBoundOfPair input[type=text].codeshard {
        padding:1px;
        font-size:11pt;
        height:20px !important;
      }
      .divOnLineWithIntegrationLimits {
        display:inline-block;
        padding-top: 15px;
        position: relative;
        left: 0px;
      }
      .divIntegrand {
        display:inline-block;
        position: relative;
        left: -8px;
      }
      .gridForPairOfIntegrationBounds {
        display:inline-grid;
        position: relative;
        top: -17px;
        left: -6px;
        grid-gap: 6px;
        text-align: left;
      }
      .lowerIntegrationBoundOfPair {
        grid-column: 1; grid-row: 2;
      }
      .upperIntegrationBoundOfPair {
        grid-column: 1; grid-row: 1;
        padding-left: 10px;
      }
   </style>",
    TeX=>""
));

# ===============================================================

#  Display the answer blanks properly in different modes

Context()->texStrings;
if ($displayMode eq 'TeX') {
   $integral1 = join("", (
      '\( \displaystyle \text{Surface Area = } \int_{ ',
      $xlow->ans_rule(5),
      ' }^{ ',
      $xhigh->ans_rule(5),
      '}\)', $xhigh->ans_rule(30), '\(\, dx \)' ));
  } else {
   $integral1 = join("", (
        openDiv( { "class" => "divOnLineWithIntegrationLimits" } ),
          '\( \displaystyle \quad\quad \text{Surface Area = } \int \)',
        closeDiv(),

        openDiv( {  "class" => "gridForPairOfIntegrationBounds" } ),
          openDiv( {  "class" => "lowerIntegrationBoundOfPair" } ),
            $xlow->ans_rule(1),
          closeDiv(),
          openDiv( {  "class" => "upperIntegrationBoundOfPair" } ),
            $xhigh->ans_rule(1),
          closeDiv(),
        closeDiv(),

        openDiv( { "class" => "divIntegrand" } ),
           $integrand->ans_rule(40),
          '\( \, dx \)',
        closeDiv(),
   ) );
}
Context()->normalStrings;


BEGIN_PGML

a) Set up the integral to compute the surface area of the solid formed by  revolving [``y = [$f]``] on the interval [`[[$xlow],[$xhigh]]`] about the [`y`]-axis. 
[`\;`]  
[@ openDiv() . $integral1 . closeDiv() @]*

b) Compute the value of the surface area. [________________]

END_PGML


WEIGHTED_ANS( Real($xlow)->cmp,5);
WEIGHTED_ANS( Real($xhigh)->cmp,5);
WEIGHTED_ANS( $integrand->cmp->withPostFilter(AnswerHints( 
  Formula("x*sqrt(1+($Df)^2)") => "This is almost correct. What are you forgetting to multiply the length by?",
  Formula("2*pi*sqrt(1+($Df)^2)") => "This is almost correct. What are you forgetting to multiply the 2`\pi` by?",
  Formula("2*pi*$f*sqrt(1+($Df)^2)") => "This looks like you may be trying to rotate around the x-axis instead of the y-axis.",
  Formula("2*pi*x*sqrt(1+$a^2*x^2)") => "This is almost correct. Check your differentiation.",
  Formula("2*pi*x*sqrt(1+($f)^2)") => "Did you take the derivative?"
)),50 );
WEIGHTED_ANS( $ans->cmp,40);

$up = 1 + 4*$a*$a*$xhigh*$xhigh;

BEGIN_PGML_SOLUTION

[``\frac{d}{dx}([$f]) = [$Df]``]. This derivative is continuous on the interval [`[[$xlow],[$xhigh]]`].

The surface area integral for revolving around the [`y`]-axis is

[``\int_{[$xlow]}^{[$xhigh]}2 \pi x\sqrt{1+f'(x)^2}\;dx =\int_{[$xlow]}^{[$xhigh]}2 \pi x\sqrt{1+\left([$Df]\right)^2}\;dx =
\int_{[$xlow]}^{[$xhigh]}2\pi x \sqrt{1+[$a*$a*4]x^2}\;dx \approx [$ans]``]

The antidifferentiation can be done using substitution with [`u = 1+[$a*$a*4]x^2`] and [`du = [$a*$a*8]x\,dx`].

[``\begin{aligned}
\int_{[$xlow]}^{[$xhigh]}2\pi x \sqrt{1+[$a*$a*4]x^2}\;dx &= 2\pi\cdot\frac{1}{[$a*$a*8]}\int_1^{[$up]}\sqrt{u}\;du \\ \\
&= \frac{\pi}{[$a*$a*4]}\cdot\frac{2}{3}u^{3/2}\bigg|_1^{[$up]} \\ \\
&= \frac{\pi}{[$a*$a*6]}\left([$up]^{3/2} - 1\right)
\end{aligned}
``]

END_PGML_SOLUTION

COMMENT('Randomization provides 96 different possible versions of this question.');

ENDDOCUMENT();