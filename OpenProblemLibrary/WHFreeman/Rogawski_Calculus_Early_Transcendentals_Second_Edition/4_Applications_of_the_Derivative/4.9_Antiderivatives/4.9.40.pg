## DBsubject(Calculus - single variable)
## DBchapter(Integrals)
## DBsection(Antiderivatives (without trigonometric functions))
## Institution(W.H.Freeman)
## Author(Brian Kell)
## MLT(antideriv_identify_graph_of_antideriv)
## Level(2)
## TitleText1('Calculus: Early Transcendentals')
## AuthorText1('Rogawski')
## EditionText1('2')
## Section1('4.9')
## Problem1('40')
## KEYWORDS('calculus', 'antiderivatives', 'graphs')

DOCUMENT();
loadMacros(
  "PGstandard.pl",
  "Parser.pl",
  "freemanMacros.pl",
  "PGgraphmacros.pl",
  "PGchoicemacros.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());

# coefficients for function ($g is the constant added to the antiderivative)
$a = non_zero_random(-2,2);
$b = random(-6,6);
$c = random(-24,24);
$d = random(-120,120);
$e = random(-720,720);
$f = random(-5040,5040);
$g = random(-5000,5000);

# # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # #
# # ALTERNATE METHOD for determining coefficients. The problem with this approach
# # is that the function tends to be rather flat.
# #
# # roots:
# # exercise in book is approximately ($r1,$r2,$r3,$r4,$r5)=(-20,-11,0,11,20)
# ($r1,$r2,$r3,$r4,$r5) = list_random_multi_uniq(5,
# 	-40, -35, -30, -25, -20, -15, 10, 5, 0, 5, 10, 15, 20, 25, 30, 35, 40
# );
# # coefficients:
# $a = list_random(-1,1);
# $b = -$a * ($r1 + $r2 + $r3 + $r4 + $r5);
# $c = $a * ($r1*$r2 + $r1*$r3 + $r1*$r4 + $r1*$r5 + $r2*$r3 + $r2*$r4 + $r2*$r5
# 		+ $r3*$r4 + $r3*$r5 + $r4*$r5);
# $d = -$a * ($r1*$r2*$r3 + $r1*$r2*$r4 + $r1*$r2*$r5 + $r1*$r3*$r4 + $r1*$r3*$r5
# 		+ $r1*$r4*$r5 + $r2*$r3*$r4 + $r2*$r3*$r5 + $r2*$r4*$r5 + $r3*$r4*$r5);
# $e = $a * ($r1*$r2*$r3*$r4 + $r1*$r2*$r3*$r5 + $r1*$r2*$r4*$r5
# 		+ $r1*$r3*$r4*$r5 + $r2*$r3*$r4*$r5);
# $f = -$a * $r1*$r2*$r3*$r4*$r5;
# $g = random(-1e8,1e8);
# # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # # #

$antideriv = Formula("($a/6)x^6+($b/5)x^5+($c/4)x^4+($d/3)x^3+($e/2)x^2+${f}x+$g");
$func = $antideriv->D("x");

@antideriv_zeros = bkell_real_zeros_finder($a/6, $b/5, $c/4, $d/3, $e/2, $f, $g);
@f_zeros = bkell_real_zeros_finder($a, $b, $c, $d, $e, $f);
@f_cp = bkell_real_zeros_finder(5*$a, 4*$b, 3*$c, 2*$d, $e);

# variables that control graph size
$margin = 0.1;
$xsize = 400;
$ysize = 400;

# find range for x
$xmin = -2;
$xmax = 2;
if (@antideriv_zeros) {
	if ($antideriv_zeros[0] < $xmin) { $xmin = $antideriv_zeros[0]; }
	if ($antideriv_zeros[-1] > $xmax) { $xmax = $antideriv_zeros[-1]; }
}
if (@f_zeros) {
	if ($f_zeros[0] < $xmin) { $xmin = $f_zeros[0]; }
	if ($f_zeros[-1] > $xmax) { $xmax = $f_zeros[-1]; }
}
$dd = $margin * ($xmax - $xmin);
$xmin -= $dd;
$xmax += $dd;

# find range for y in function graph
$f_ymin = $f_ymax = 0;
for $x ($xmin, @f_cp, $xmax) {
	$y = $func->eval(x=>$x);
	if ($y < $f_ymin) { $f_ymin = $y; }
	elsif ($y > $f_ymax) { $f_ymax = $y; }
}
$dd = $margin * ($f_ymax - $f_ymin);
$f_ymin -= $dd;
$f_ymax += $dd;

# find range for y in antiderivative graph
$a_ymin = $a_ymax = 0;
for $x ($xmin, @f_zeros, $xmax) {
	$y = $antideriv->eval(x=>$x);
	if ($y < $a_ymin) { $a_ymin = $y; }
	elsif ($y > $a_ymax) { $a_ymax = $y; }
}
$dd = $margin * ($a_ymax - $a_ymin);
$a_ymin -= $dd;
$a_ymax += $dd;

# ensure graphs have the same scale on y-axis
$f_diff = $f_ymax - $f_ymin;
$a_diff = $a_ymax - $a_ymin;
$dd = abs($f_diff - $a_diff);
if ($f_diff > $a_diff) {
	$a_ymin -= 0.5 * $dd;
	$a_ymax += 0.5 * $dd;
} else {
	$f_ymin -= 0.5 * $dd;
	$f_ymax += 0.5 * $dd;
}

$f_graph = init_graph(
    $xmin, $f_ymin,
    $xmax, $f_ymax,
    axes => [0,0],
    size => [$xsize,$ysize]
);
plot_functions($f_graph,
	"$func for x in [$xmin,$xmax] using color:red and weight:2"
);

$a_graph = init_graph(
	$xmin, $a_ymin,
	$xmax, $a_ymax,
	axes => [0,0],
    size => [$xsize,$ysize]
);
plot_functions($a_graph,
	"$antideriv for x in [$xmin,$xmax] using color:red and weight:2"
);

$b_graph = init_graph(
	$xmin, -$a_ymax,
	$xmax, -$a_ymin,
	axes => [0,0],
	size => [$xsize,$ysize]
);
plot_functions($b_graph,
	"-$antideriv for x in [$xmin,$xmax] using color:red and weight:2"
);

$f_image = image(insertGraph($f_graph));
$a_image = image(insertGraph($a_graph));
$b_image = image(insertGraph($b_graph));

$mc = new_multiple_choice();
$mc->qa(<<"KONEC", $a_image);
Which of the following is the graph of an antiderivative of \( f(x) \), shown
below?
$PAR
\( f(x) \): $f_image
KONEC
$mc->extra($b_image);

Context()->texStrings;
BEGIN_TEXT
\{ textbook_ref_exact("Rogawski ET 2e", "4.9", "40") \}
$PAR
\{ $mc->print_q() \}
$BR
\{ $mc->print_a() \}
END_TEXT
Context()->normalStrings;

ANS(radio_cmp($mc->correct_ans));

@f_sign_x = ($xmin);
if (@f_zeros) {
	for ($i = 0; $i < $#f_zeros; ++$i) {
		push @f_sign_x, ($f_zeros[$i] + $f_zeros[$i+1]) / 2;
	}
	push @f_sign_x, $xmax;
}
@f_sign = map( ("0","+","-")[bkell_poly_eval($_,$a,$b,$c,$d,$e,$f)<=>0], @f_sign_x );
%map = ( "0" => "flat", "+" => "increasing", "-" => "decreasing" );
@antideriv_slope = map $map{$_}, @f_sign;
if (@f_sign > 1) {
	$f_behavior = "Since, moving left to right, \(f(x)\) transitions from \(".
		(join "\) to \(", @f_sign)."\)";
	$antideriv_behavior = "transition from ".(join " to ", @antideriv_slope);
} else {
	$f_behavior = "Since \(f(x)\) is always ".
		($f_sign[0] eq "+" ? "positive" : $f_sign[0] eq "-" ? "negative" : "zero");
	$antideriv_behavior = "be $antideriv_slope[0] everywhere";
}
$correct = $mc->correct_ans;

Context()->texStrings;
SOLUTION(EV3(<<'END_SOLUTION'));
$PAR
$SOL
Let \(F(x)\) be an antiderivative of \(f(x)\).  By definition, this means
\(F'(x)=f(x)\).  In other words, \(f(x)\) provides information as to the
increasing/decreasing behavior of \(F(x)\).  $f_behavior, it follows that
\(F(x)\) must $antideriv_behavior.  This describes the graph in ($correct)!
END_SOLUTION

ENDDOCUMENT();
