## DBsubject(Algebra)
## DBchapter(Linear equations and functions)
## DBsection(Linear functions)
## Institution(W.H.Freeman)
## Author(Brian Kell)
## MLT(Is_it_linear_from_table)
## Level(3)
## TitleText1('Calculus: Early Transcendentals')
## AuthorText1('Rogawski')
## EditionText1('2')
## Section1('1.2')
## Problem1('31')
## KEYWORDS('calculus', 'algebra', 'arithmetic sequence', 'data fitting', 'experimental data', 'linear equations', 'nonlinear equations', 'slope', 'table question', 'true/false')

DOCUMENT();
loadMacros(
  "PGstandard.pl",
  "Parser.pl",
  "freemanMacros.pl",
  "PGchoicemacros.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());

$problem = random(1,6);  # choose a problem

{  # start bare block

if ($problem == 1)  {  ##### period of a pendulum #############################

	$problem_text = <<"KONEC";
The period \(T\) of a pendulum is measured for pendulums of several different
lengths \(L\). Based on the following data, does \(T\) appear to be a linear
function of \(L\)?
KONEC
	$si = random(0,1);
	$xvar = "L";
	$xlabel = "\(L\) (".($si ? "m" : "ft").")";
	$yvar = "T";
	$ylabel = "\(T\) (s)";
	$x = random(1,8);
	$step = random(1,3);
	@xval = ($x, $x + $step, $x + 2*$step, $x + 3*$step);
	@yval = ();
	for $l (@xval) {
		push @yval, sprintf(
			"%.2f", 6.2831853071796 * sqrt(   # 6.28... = 2*pi
				$l / ($si ? 9.80665 : 32.1740485564)  # value for g
			)
		);
	}
	$answer = 'no';

} elsif ($problem == 2) {  ##### Ohm's law ####################################

	$problem_text = <<"KONEC";
The electrical current \(I\) flowing through a wire is measured when different
voltages \(V\) are applied. Based on the following data, does \(I\) appear to
be a linear function of \(V\)?
KONEC
	$xvar = "V";
	$xlabel = "\(V\) (V)";
	$yvar = "I";
	$ylabel = "\(I\) (A)";
	$r = random(0.1, 10, 0.01);  # resistance
	$x = random(0.5, 4, 0.5);
	$step = random(0.5, 2, 0.5);
	@xval = ($x, $x + $step, $x + 2*$step, $x + 3*$step);
	@yval = ();
	for $v (@xval) {
		push @yval, sprintf("%.3f", $v/$r);
	}
	$answer = 'yes';

} elsif ($problem == 3) {  ##### Hooke's law ##################################

	$problem_text = <<"KONEC";
The force \(F\) exerted by a metal spring is measured when the spring is
stretched by different lengths \(L\). Based on the following data, does \(F\)
appear to be a linear function of \(L\)?
KONEC
	$si = random(0,1);
	$xvar = "L";
	$xlabel = "\(L\) (".($si ? "cm" : "in").")";
	$yvar = "F";
	$ylabel = "\(F\) (".($si ? "N" : "lb").")";
	$k = random(0.05, 1, 0.01);  # spring constant
	$x = random(1,10);
	$step = random(1,4);
	@xval = ($x, $x + $step, $x + 2*$step, $x + 3*$step);
	@yval = ();
	for $l (@xval) {
		push @yval, sprintf("%.2f", $l*$k);
	}
	$answer = 'yes';

} elsif ($problem == 4) {  ##### sorting ######################################
	
	$problem_text = <<"KONEC";
The average time \(t\) required to sort the records in a computer database is
measured as the number \(N\) of records in the database increases. Based on the
following data, does \(t\) appear to be a linear function of \(N\)?
KONEC
	$xvar = "N";
	$xlabel = "\(N\) (thousands)";
	$yvar = "t";
	$ylabel = "\(t\) (ms)";
	$ns = random(10, 1000, 1);  # nanoseconds per comparison
	$x = random(10,100,10);
	$step = random(5,20,5);
	@xval = ($x, $x + $step, $x + 2*$step, $x + 3*$step);
	@yval = ();
	for $n (@xval) {
		push @yval, sprintf("%.2f", $ns * $n * (log($n*1000))/log(2) / 1000);
	}
	$answer = 'no';

} elsif ($problem == 5) {  ##### hydrostatic pressure #########################
	
	$problem_text = <<"KONEC";
The water pressure \(P\) is measured at various depths \(D\) in the ocean.
Based on the following data, does \(P\) appear to be a linear function of
\(D\)?
KONEC
	$si = random(0,1);
	$xvar = "D";
	$xlabel = "\(D\) (".($si ? "m" : "ft").")";
	$yvar = "P";
	$ylabel = "\(P\) (".($si ? "kPa" : "psi").")";
	$x = random(100,1000,100);
	$step = random(100,1000,100);
	@xval = ($x, $x + $step, $x + 2*$step, $x + 3*$step);
	@yval = ();
	for $d (@xval) {
		push @yval, sprintf("%.0f",
			($si ? 1024 : 2)  # density of seawater (kg/m^3, slug/ft^3)
			* ($si ? 9.80665 : 32.1740485564)  # g
			* $d
			/ ($si ? 1000 : 144)  # convert to kPa or psi
			+ ($si ? 101.325 : 14.696)  # add atmospheric pressure
		);
	};
	$answer = 'yes';

} elsif ($problem == 6) {  ##### radioactive decay ############################

	($isotope, $half_life, $hl_units) = @{list_random(
		# these all decay to stable isotopes, so that we do not have to
		# consider the radioactivity of daughter isotopes
		[ "polonium-210", 138.38, "days" ],
		[ "thallium-206", 4.20, "days" ],
		[ "polonium-211", 0.516, "s" ],
		[ "thallium-207", 288, "s" ],
		[ "polonium-212", 299, "ns" ],
		[ "thallium-208", 183.18, "s" ],
		# [ "carbon-14", 5760, "y" ],  # half-life too long
		[ "lead-209", 3.253, "hr" ],
	)};
	$problem_text = <<"KONEC";
The radioactivity \(A\) of a small sample of $isotope is measured at various
times \(t\). Based on the following data, does \(A\) appear to be a linear
function of \(t\)?
KONEC
	$xvar = "t";
	$xlabel = "\(t\) ($hl_units)";
	$yvar = "A";
	$ylabel = "\(A\) (Ci)";
	$decay_constant = log(2) / $half_life;
	$tau = 1 / $decay_constant;
	$N_0 = random(1, 1000, 0.1);  # initial amount of substance, measured in curies
	$time_quantum = bkell_125($half_life / 4);
	$x = random(0, 4) * $time_quantum;
	$step = random(1, 4) * $time_quantum;
	@xval = ($x, $x + $step, $x + 2*$step, $x + 3*$step);
	@yval = ();
	for $t (@xval) {
		push @yval, sprintf("%.2f", $decay_constant * $N_0 * exp(-$t/$tau));
	};
	$answer = 'no';

} else {  ##### do pendulum problem if we get here somehow, just in case ######

	$problem = 1;
	redo;  # go back to top of bare block

}

}  # end bare block

$table_begin = begintable(1 + scalar @xval);
$table_row1 = row($xlabel, @xval);
$table_row2 = row($ylabel, @yval);
$table_end = endtable();

$problem_text_with_data = $problem_text.<<"KONEC";
$PAR
$table_begin
$table_row1
$table_row2
$table_end
KONEC

$mc = new_multiple_choice();
$mc->qa($problem_text_with_data, ($answer eq 'yes' ? 'Yes' : 'No'));
$mc->makeLast('Yes', 'No');

Context()->texStrings;
BEGIN_TEXT
\{ textbook_ref_exact("Rogawski ET 2e", "1.2", "31") \}
$PAR
\{ $mc->print_q() \}
$BR
\{ $mc->print_a() \}
END_TEXT
Context()->normalStrings;

ANS(radio_cmp($mc->correct_ans));

for ($i = 1; $i <= 3; ++$i) {
	${"slope$i"} = ($yval[$i] - $yval[$i-1]) / ($xval[$i] - $xval[$i-1]);
	${"slope${i}_approx"} = bkell_sigfigs(${"slope$i"}, 4);
	if (${"slope${i}_approx"} == 0 ? ${"slope$i"} == 0 :
			abs(${"slope$i"}/${"slope${i}_approx"} - 1.0) < 1e-8) {
		${"slope${i}_eq"} = "=";
		${"slope${i}_approx"} += 0;
	} else {
		${"slope${i}_eq"} = "\approx";
	}
}

if ($answer eq 'yes') {
	if ($slope1_approx == $slope2_approx && $slope2_approx == $slope3_approx) {
		$conclusion = <<"KONEC";
Because the three slopes are equal, \($yvar\) appears to be a linear function
of \($xvar\).
KONEC
	} else {
		$conclusion = <<"KONEC";
Because the three slopes are very nearly equal (the small differences are
likely due to measurement error), \($yvar\) appears to be a linear function of
\($xvar\).
KONEC
	}
} else {
	$conclusion = <<"KONEC";
Because the three slopes are not equal, \($yvar\) does not appear to be a
linear function of \($xvar\).
KONEC
}

Context()->texStrings;
SOLUTION(EV3(<<'END_SOLUTION'));
$PAR
$SOL
$PAR
Examine the slope between consecutive data points.  The first pair of data
points yields a slope of
\[ { $yval[1]-$yval[0] \over $xval[1]-$xval[0] } $slope1_eq $slope1_approx, \]
while the second pair of data points yields a slope of 
\[ { $yval[2]-$yval[1] \over $xval[2]-$xval[1] } $slope2_eq $slope2_approx, \]
and the last pair of data points yields a slope of 
\[ { $yval[3]-$yval[2] \over $xval[3]-$xval[2] } $slope3_eq $slope3_approx. \]
$conclusion
END_SOLUTION

ENDDOCUMENT();
