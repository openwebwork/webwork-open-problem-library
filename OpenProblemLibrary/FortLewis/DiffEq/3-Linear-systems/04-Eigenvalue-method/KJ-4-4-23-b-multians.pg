## DESCRIPTION
## Eigenvalues and systems of differential equations
## ENDDESCRIPTION


## DBsubject(Differential equations)
## DBchapter(Systems of differential equations)
## DBsection(Distinct real eigenvalues)
## Date(01/31/2012)
## Institution(Rochester and Fort Lewis College)
## Author(Paul Pearson)
## MLT(SDE-DRE-LI)
## Level(4)
## MO(1)
## TitleText1('Elementary Differential Equations')
## AuthorText1('Kohler and Johnson')
## EditionText1('2006')
## Section1('4.4')
## Problem1('23')


##################################
#  Initialization

DOCUMENT();

loadMacros(
  "PGstandard.pl",
  "PGmatrixmacros.pl",
  "MathObjects.pl",
  "parserMultiAnswer.pl",
  "unionLists.pl",
  "parserPopUp.pl",
  "weightedGrader.pl",
  "PGcourse.pl"
);


TEXT(beginproblem());

install_weighted_grader();

SRAND($psvn);


#####################################
#  Setup

Context("Numeric")->variables->are(t=>"Real");

# dilation
$a = non_zero_random(-3,3,1);

$A11 = -5 * $a;  $A12 = -6 * $a;
$A21 =  3 * $a;  $A22 =  4 * $a;

$lambda1 =  Real("-2 * $a");
$v11 = Real("-2");
$v21 = Real("1");

$lambda2 = Real("1 * $a");
$v12 = Real("1");
$v22 = Real("-1");

$y1top = Formula("$v11 e^($lambda1 t)");
$y1bot = Formula("$v21 e^($lambda1 t)");

$y2top = Formula("$v12 e^($lambda2 t)");
$y2bot = Formula("$v22 e^($lambda2 t)");


$multians2 = MultiAnswer($y1top, $y1bot, $y2top, $y2bot)->with(

    singleResult => 1,
    checkTypes => 0,
    allowBlankAnswers => 1,
    format =>  
    "<table border='0' cellspacing='0'>
    <tr>
      <td><table border='0' cellspacing='5'><tr><td> %s </td></tr><tr><td> %s </td></tr></table></td>
      <td>&nbsp;&nbsp;</td>
      <td><table border='0' cellspacing='5'><tr><td> %s </td></tr><tr><td> %s </td></tr></table></td>
    </tr>
    </table>",
    tex_format =>  
    "\left\lbrack \begin{array}{r} %s \\ %s \end{array}\right\rbrack, \ \ 
     \left\lbrack \begin{array}{r} %s \\ %s \end{array}\right\rbrack",

    checker => sub {
        my ($correct, $student, $answerHash ) = @_;
        my @c = @{$correct};
	my @s = @{$student};

        my $flag1 = 0;
        my $flag2 = 0;
        my $flag  = 0;

        Value::Error("Your solutions are not linearly independent") if
        $c[0]->typeMatch($s[0]) &&
        $c[1]->typeMatch($s[1]) &&
        $c[2]->typeMatch($s[2]) &&
        $c[3]->typeMatch($s[3]) &&
        ($s[0] * $s[3] == $s[1] * $s[2]);

        if ( $c[0]->typeMatch($s[0]) && 
             $c[1]->typeMatch($s[1]) && 
             ($s[0] != 0 || $s[1] != 0) ) 
        {
          if ( ($A11 * $s[0] + $A12 * $s[1] == $s[0]->D('t')) &&
               ($A21 * $s[0] + $A22 * $s[1] == $s[1]->D('t')) 
          ){
                $flag1=1;
          }
        }

        if ( $c[2]->typeMatch($s[2]) && 
             $c[3]->typeMatch($s[3]) && 
             ($s[2] != 0 || $s[3] != 0) ) 
        {
          if ( ($A11 * $s[2] + $A12 * $s[3] == $s[2]->D('t')) &&
               ($A21 * $s[2] + $A22 * $s[3] == $s[3]->D('t')) 
          ){
                $flag2=1;
          }
        }

        if($flag1==1 && $flag2 ==1){
           $flag=1;
        }else{
           if ($flag1 == 1) {
              Value::Error("The solution on the left is correct");
           } elsif ($flag2 == 1) {
              Value::Error("The solution on the right is correct");
           }else{
              Value::Error("Neither of the solutions is correct");
           }
        }
        return $flag; 
       
    }
);


$popup = PopUp(["Choose","Yes, it is a fundamental set","No, it is not a fundamental set"],"Yes, it is a fundamental set");


#####################################
#  Main text

Context()->texStrings;
BEGIN_TEXT
Consider the linear system
$PAR
\[ 
\displaystyle
\boldsymbol{ \vec{y}^{\,\prime} }
=
\left\lbrack
\begin{array}{rr}
$A11 & $A12 \\
$A21 & $A22
\end{array}
\right\rbrack
\boldsymbol{\vec{y}}
\]
$BR

\{ BeginList("OL",type=>"a") \}
$ITEM
Find the eigenvalues and eigenvectors for 
the coefficient matrix.  Then, form  
solutions to 
\( \boldsymbol{\vec{y}^{\,\prime}} = A \boldsymbol{\vec{y}} \) 
for each eigenpair and enter your solutions below. Use \( t \)
as the independent variable in your answers.
$BCENTER
\{ 
mbox(
[
'\( \boldsymbol{\vec{y}_1}(t) =  \) ', 
display_matrix([[$multians2->ans_rule(10)],[$multians2->ans_rule(10)]]),
' and \( \boldsymbol{\vec{y}_2}(t) =  \) ', 
display_matrix([[$multians2->ans_rule(10)],[$multians2->ans_rule(10)]]),
],
allowbreaks=>'yes'
) 
\}
$ECENTER

$ITEMSEP
$ITEM
Does the set of solutions you found form a 
fundamental set (i.e., linearly independent set)
of solutions?
\{ $popup->menu() \}

\{ EndList("OL") \}
END_TEXT
Context()->normalStrings;

#####################################
#  Answer evaluation

$showPartialCorrectAnswers = 1;

WEIGHTED_ANS( $multians2->cmp(), 98 );
WEIGHTED_ANS( $popup->cmp(), 2 );

COMMENT('Randomized with distinct real eigenvalues and integral answers.');

ENDDOCUMENT(); 
