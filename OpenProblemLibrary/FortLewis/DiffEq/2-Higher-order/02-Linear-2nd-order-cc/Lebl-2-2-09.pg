## DESCRIPTION
## Higher order ODEs: constant coefficient second order homogeneous linear ODEs
## ENDDESCRIPTION


## DBsubject(Differential equations)
## DBchapter(Higher order differential equations)
## DBsection(Linear, constant coefficients, homogeneous (distinct real roots))
## Date(01/30/2011)
## Institution(Fort Lewis College)
## Author(Paul Pearson)
## Level(3)
## MO(1)
## TitleText1('Notes on Diffy Qs')
## AuthorText1('Jiri Lebl')
## EditionText1('December 9, 2010')
## Section1('2.2')
## Problem1('9')
## KEYWORDS('differential equations','second order linear ODE','constant coefficients')


##############################
#  Initialization

DOCUMENT(); 

loadMacros(
"PGstandard.pl",
"MathObjects.pl",
"AnswerFormatHelp.pl",
"parserAssignment.pl",
);

TEXT(beginproblem());


#############################
#  Setup

Context("Numeric");
Context()->variables->add(
'c1'=>"Real",'c2'=>"Real",y=>"Real",
);
Context()->variables->set('c1'=>{limits=>[2,4]},'c2'=>{limits=>[2,4]});
Context()->flags->set(
    formatStudentAnswer=>'parsed',
    reduceConstants=>0,
    reduceConstantFunctions=>0,
);
parser::Assignment->Allow;

$a  = random(2,9,1);
$ap1 = $a + 1;

# char poly r(r+$a)

$diffeq = "y^{\,\prime\prime} + $a y^{\,\prime} = 0"; # tex

$answera = Compute("y = c1 + c2 e^(-$a x)");

$answerb = Compute("y = $ap1/$a - 1/$a e^(-$a x)");

######################
#  Main text

Context()->texStrings;
BEGIN_TEXT
(a) Find the general solution to \( $diffeq \).
Give your answer as \( y = . . . \ \). 
In your answer, use \( c_1 \) and \( c_2 \) to 
denote arbitrary constants and \( x \) 
the independent variable.  Enter \( c_1 \) as 
${BTT}c1${ETT} and \( c_2 \) as ${BTT}c2${ETT}.
$BR
\{ ans_rule(40) \}
\{ AnswerFormatHelp("equations") \}
$BR
$BR
(b) Find the particular solution that satisfies
\( y(0) = 1 \) and \( y^{\,\prime}(0) = 1 \).
$BR
\{ ans_rule(40) \}
\{ AnswerFormatHelp("equations") \}
END_TEXT
Context()->normalStrings;


######################
#  Answer evaluation

$showPartialCorrectAnswers = 1;

ANS( $answera->cmp( checker => sub {
    my ( $correct, $student, $answerHash ) = @_;
    my $stu   = Formula($student->{tree}{rop});

    ################################
    #  Check for arbitrary constants
    #
    Value->Error("Is your answer the most general solution?") 
    if ( 
      Formula($stu->D('c1'))==Formula(0) || 
      Formula($stu->D('c2'))==Formula(0) 
    );

    ############################################
    #  Check for linear independence (Wronskian)
    #
    my $x = Real(1.24);

    my $a11 = $stu->eval('c1'=>1,'c2'=>0,x=>$x,y=>0);
    my $a12 = $stu->eval('c1'=>0,'c2'=>1,x=>$x,y=>0);
 
    my $a21 = $stu->D('x')->eval('c1'=>1,'c2'=>0,x=>$x,y=>0);
    my $a22 = $stu->D('x')->eval('c1'=>0,'c2'=>1,x=>$x,y=>0);

    Value->Error("Your functions are not linearly independent or your answer is not complete") 
    if ($a11 * $a22 == $a21 * $a12);

    # my $wronskian = $a11*$a22 - $a21*$a12;
    # Value->Error("Your functions are not linearly independent or your answer is not complete") 
    # if ($wronskian==Real(0));

    ########################################################
    #  Check that the student answer is a solution to the DE
    #
    my $stu1 = Formula($stu->D('x'));
    my $stu2 = Formula($stu->D('x','x'));
    return ($stu2) == (-1 * $a * $stu1);

    # my $stuDE = Formula($stuxx + $a*$stux)
    # ->with(test_points=>[[1,1,0.1,0],[2,1,0,0],[1,2,-0.1,0],[1,1,0,1]]);
    # return ($stuDE==Formula(0));


}));

ANS( $answerb->cmp() );

COMMENT("MathObject version.  Characteristic polynomial r(r + a).");

ENDDOCUMENT();