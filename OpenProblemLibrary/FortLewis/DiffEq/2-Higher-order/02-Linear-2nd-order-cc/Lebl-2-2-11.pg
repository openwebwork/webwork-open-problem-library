## DESCRIPTION
## Higher order ODEs: constant coefficient second order homogeneous linear ODEs
## ENDDESCRIPTION


## DBsubject(Differential equations)
## DBchapter(Higher order differential equations)
## DBsection(Linear, constant coefficients, homogeneous (complex roots))
## Date(01/30/2011)
## Institution(Fort Lewis College)
## Author(Paul Pearson)
## Level(3)
## MO(1)
## TitleText1('Notes on Diffy Qs')
## AuthorText1('Jiri Lebl')
## EditionText1('December 9, 2010')
## Section1('2.2')
## Problem1('11')
## KEYWORDS('differential equations','second order linear ODE','constant coefficients')


##############################
#  Initialization

DOCUMENT(); 

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "AnswerFormatHelp.pl",
  "parserAssignment.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());


#############################
#  Setup

Context("Numeric");
Context()->variables->add(
'c1'=>"Real",'c2'=>"Real",y=>"Real",
);
Context()->variables->set('c1'=>{limits=>[2,4]},'c2'=>{limits=>[2,4]});
Context()->flags->set(
    formatStudentAnswer=>'parsed',
    reduceConstants=>0,
    reduceConstantFunctions=>0,
);
parser::Assignment->Allow;

$a  = random(2,5,1);
$aa = $a**2;
$a2 = 2*$a;
do { $b = random(2,5,1); } until ($b != $a);
$bb = $b**2;
$c = $aa + $bb;

$s = random(-1,1,2);
$msa = -($s) * $a;

# char poly (r + $s $a)^2 + $b^2

if ($s==1) {
  $diffeq = "y^{\,\prime\prime} + $a2 y^{\,\prime} + $c y = 0"; # tex
} else {
  $diffeq = "y^{\,\prime\prime} - $a2 y^{\,\prime} + $c y = 0"; # tex
}


$answer = Compute("y = c1 e^($msa x) cos($b x) + c2 e^($msa x) sin($b x)");


######################
#  Main text

Context()->texStrings;
BEGIN_TEXT
Find the general solution to \( $diffeq \).
Give your answer as \( y = . . .  \).
In your answer, use \( c_1 \) and \( c_2 \) to 
denote arbitrary constants and \( x \) 
the independent variable.  Enter \( c_1 \) as
${BTT}c1${ETT} and \( c_2 \) as ${BTT}c2${ETT}.
$BR
$BR
\{ ans_rule(40) \}
\{ AnswerFormatHelp("equations") \}
END_TEXT
Context()->normalStrings;


######################
#  Answer evaluation

$showPartialCorrectAnswers = 1;
ANS( $answer->cmp( checker => sub {
    my ( $correct, $student, $answerHash ) = @_;
    my $stu   = Formula($student->{tree}{rop});

    ################################
    #  Check for arbitrary constants
    #
    Value->Error("Is your answer the most general solution?") 
    if ( 
      Formula($stu->D('c1'))==Formula(0) || 
      Formula($stu->D('c2'))==Formula(0) 
    );

    ############################################
    #  Check for linear independence (Wronskian)
    #
    my $x = Real(1.24);

    my $a11 = $stu->eval('c1'=>1,'c2'=>0,x=>$x,y=>0);
    my $a12 = $stu->eval('c1'=>0,'c2'=>1,x=>$x,y=>0);
 
    my $a21 = $stu->D('x')->eval('c1'=>1,'c2'=>0,x=>$x,y=>0);
    my $a22 = $stu->D('x')->eval('c1'=>0,'c2'=>1,x=>$x,y=>0);

    # my $wronskian = $a11*$a22 - $a21*$a12;
    # Value->Error("Your functions are not linearly independent or your answer is not complete") 
    # if ($wronskian==Real(0));

    Value->Error("Your functions are not linearly independent or your answer is not complete") 
    if ($a11 * $a22 == $a21 * $a12);


    ########################################################
    #  Check that the student answer is a solution to the DE
    #
    my $stu1 = Formula($stu->D('x'));
    my $stu2 = Formula($stu->D('x','x'));
    return ($stu2 + $s * $a2 * $stu1) == (-1 * $c * $stu);

    #my $stuDE = Formula($stuxx + $s*$a2*$stux + $c*$stu)
    #->with(test_points=>[[1,1,0.1,0],[2,1,0,0],[1,2,-0.1,0],[1,1,0,1]]);
    #return ($stuDE==Formula(0));


}));


COMMENT("Characteristic polynomial (r + s a)^2 + b^2");

ENDDOCUMENT();
