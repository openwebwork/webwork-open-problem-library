## DESCRIPTION
## Eigenvalues and eigenvectors of 2x2 matrix (distinct, real)
## ENDDESCRIPTION


## DBsubject(Linear algebra)
## DBchapter(Eigenvalues and eigenvectors)
## DBsection(Computing eigenvalues and eigenvectors)
## Date(11/06/2015)
## Institution(METU-NCC)
## Author(Benjamin Walter)
## Level(4)
## Static(0)
## MO(1)


##################################
#  Initialization

DOCUMENT();

loadMacros(
  "PGstandard.pl",
  "PGmatrixmacros.pl",
  "MathObjects.pl",
  "parserMultiAnswer.pl",
  "PGcourse.pl"
);


TEXT(beginproblem());

#####################################
#  Setup

Context("Complex");

# 1) choose eigenvalues a +- bi for matrix of form
#    [ 0  -c ]     [  0   c ]
#    [ d  2a ]  or [ -d  2a ]
#
#  where a^2 + b^2 = cd  possibilities are in @CNum and @OffD
#
# 2) phase shift to get 
#    [ p   -c  ]
#    [ d  2a+p ]
#
#  eigenvalues (p+a)+-bi 
#  eigenvectors [ -c  a+-bi ] 

@CNum = ([1,1, 1,1, 1, 1, 1,2,2, 2, 2,3,3,3,3,4, 4, 4, 4, 5, 6,6, 6],
         [1,2, 3,3, 5, 7, 8,2,2, 4, 6,3,3,4,6,4, 6, 7, 8, 5, 6,6, 8]);
@OffD = ([1,1, 1,2, 2, 5, 5,1,2, 2, 4,2,3,5,5,4, 4, 5, 8, 5, 6,8,10],
         [2,5,10,5,13,10,13,8,4,10,10,9,6,5,9,8,13,13,10,10,12,9,10]);
$i = random(0,22,1);
$j = random(0,1,1); 
$k = random(0,1,1); 
$sign1 = non_zero_random(-1,1,1);
$sign2 = non_zero_random(-1,1,1);

$a = $sign1*$CNum[$j][$i];
$b =        $CNum[($j+1)%2][$i];

$c = $sign2*$OffD[$k][$i];
$d = $sign2*$OffD[($k+1)%2][$i];

#
# choose phase so that real part is between -6 and 6.
#
$phase = non_zero_random( max(-6-$a,-9) , min(6-$a,9) , 1);

$A11 = $phase;  $A12 =  -$c;
$A21 = $d;      $A22 =  2*$a+$phase;

$lambda1 = Complex("$phase + $a + $b i");
$v11 = Complex("-$c");
$v21 = Complex("$a + $b i");

$lambda2 = Complex("$phase + $a - $b i");
$v12 = Complex("-$c");
$v22 = Complex("$a - $b i");


$multians1 = MultiAnswer($lambda1, $v11, $v21, $lambda2, $v12, $v22)->with(
    singleResult => 1,
    checkTypes => 0,
    allowBlankAnswers => 1,
    format =>  
    "<table border='0' cellspacing='10'>
    <tr>
      <td> %s, \(\Bigl\lbrack\)</td>
      <td><table border='0' cellspacing='10'><tr><td> %s </td></tr><tr><td> %s </td></tr></table></td>
      <td>\(\Bigr\rbrack\);&nbsp;&nbsp; %s, \(\Bigl\lbrack\)</td>
      <td><table border='0' cellspacing='10'><tr><td> %s </td></tr><tr><td> %s </td></tr></table></td>
      <td>\(\Bigr\rbrack\)</td>
    </tr>
    </table>",
    tex_format =>  
    "%s, \left\lbrack \begin{array}{r} %s \\ %s \end{array}\right\rbrack; \ \ 
     %s, \left\lbrack \begin{array}{r} %s \\ %s \end{array}\right\rbrack",

    checker => sub {
        my ($correct, $student, $answerHash ) = @_;
        my @c = @{$correct};
	my @s = @{$student};

        my $flag1 = 0;
        my $flag2 = 0;
        my $flag  = 0;

        Value::Error("Your vectors are parallel") if
        $c[1]->typeMatch($s[1]) &&
        $c[2]->typeMatch($s[2]) &&
        $c[4]->typeMatch($s[4]) &&
        $c[5]->typeMatch($s[5]) &&
        ($s[1] * $s[5] == $s[2] * $s[4]);
        
        if ( $c[0]->typeMatch($s[0]) && 
             $c[1]->typeMatch($s[1]) && 
             $c[2]->typeMatch($s[2]) && 
             ($s[1] != 0 || $s[2] != 0) ) 
        {
          if ( ($A11 * $s[1] + $A12 * $s[2] == $s[0] * $s[1]) &&
               ($A21 * $s[1] + $A22 * $s[2] == $s[0] * $s[2]) 
          ){
                $flag1=1;
          }
        }

        if ( $c[3]->typeMatch($s[3]) && 
             $c[4]->typeMatch($s[4]) && 
             $c[5]->typeMatch($s[5]) && 
             ($s[4] != 0 || $s[5] != 0) ) 
        {
          if ( ($A11 * $s[4] + $A12 * $s[5] == $s[3] * $s[4]) &&
               ($A21 * $s[4] + $A22 * $s[5] == $s[3] * $s[5]) 
          ){
                $flag2=1;
          }
        }

        if($flag1==1 && $flag2 ==1){
           $flag=1;
        }else{
           if ($flag1 == 1) {
              my $msg = "\((\lambda_1, \boldsymbol{\vec{v}_1})\) is correct; ";
              if (($c[3]->typeMatch($s[3])) && (($s[3] == $c[0]) || ($s[3] == $c[3])) && ($s[0] != $s[3])) {
                $msg = $msg . "and \(\lambda_2\) is correct but \(\boldsymbol{\vec{v}_2}\) is incorrect";
              } else {
                $msg = $msg . "but \(\lambda_2\) and \(\boldsymbol{\vec{v}_2}\) are incorrect";
              }
              Value::Error($msg);
           } elsif ($flag2 == 1) {
              my $msg = "\((\lambda_2, \boldsymbol{\vec{v}_2})\) is correct; ";
              if (($c[0]->typeMatch($s[0])) && (($s[0] == $c[0]) || ($s[0] == $c[3])) && ($s[0] != $s[3])) {
                $msg = $msg . "and \(\lambda_1\) is correct but \(\boldsymbol{\vec{v}_1}\) is incorrect";
              } else {
                $msg = $msg . "but \(\lambda_1\) and \(\boldsymbol{\vec{v}_1}\) are incorrect";
              }
              Value::Error($msg);
            } else {
              my $msg = "";
              if (($c[0]->typeMatch($s[0])) && (($s[0] == $c[0]) || ($s[0] == $c[3]))) {
                $msg = $msg . "\(\lambda_1\) is correct; ";
              } if (($c[3]->typeMatch($s[3])) && (($s[3] == $c[0]) || ($s[3] == $c[3])) && ($s[0] != $s[3])) {
                $msg = $msg . "\(\lambda_2\) is correct; ";
              }
              Value::Error($msg . "both eigenvectors are wrong");
            }
        }
        return $flag; 
       
    }
);



#####################################
#  Main text

Context()->texStrings;
BEGIN_TEXT
Find the eigenvalues and eigenvectors of the matrix
\( 
\displaystyle
\mathrm{A} = 
\left\lbrack
\begin{array}{rr}
$A11 & $A12 \\
$A21 & $A22
\end{array}
\right\rbrack
\)
\{ mbox( [ 
'\( \lambda_1 = \) ',
$multians1->ans_rule(5),
', \( \boldsymbol{\vec{v}_1} =  \) ', 
display_matrix([[$multians1->ans_rule(5)],[$multians1->ans_rule(5)]])
] ) \}
and 
\{ mbox( [
'\( \lambda_2 = \) ',
$multians1->ans_rule(5),
', \( \boldsymbol{\vec{v}_2} =  \) ', 
display_matrix([[$multians1->ans_rule(5)],[$multians1->ans_rule(5)]]),
] ) \}

END_TEXT
Context()->normalStrings;

#####################################
#  Answer evaluation

$showPartialCorrectAnswers = 1;

ANS( $multians1->cmp() );

COMMENT('MathObject version.  Randomized with complex integer eigenvalues.');

ENDDOCUMENT(); 
