#Problem APEX 6.1.56-57

## DBsubject(Calculus - single variable)
## DBchapter(Techniques of integration)
## DBsection(Substitution (without trigonometric functions))
## Institution(Valdosta State University)
## Author(S. V. Ault)
## Level(3)
## MO(1)
## TitleText1('APEX Calculus')
## AuthorText1('Hartman')
## EditionText1('3.0')
## Section1('6.1')
## Problem1('56 57')

DOCUMENT();

# Load whatever macros you need for the problem
loadMacros(
    'PGstandard.pl',      'PGML.pl',
    'contextFraction.pl', 'parserFormulaUpToConstant.pl',
    'answerHints.pl',     'PGcourse.pl'
);

## (a) ##
$a1 = non_zero_random(-9, 9);

$u1 = Formula("x + $a1")->reduce;

$f1 = Formula("1/$u1");

## (b) ##
$a2 = random(2, 20);
$b2 = random(2, 9) * random(-1, 1, 2);
$c2 = non_zero_random(-20, 20);

$u2 = Formula("$b2 x + $c2")->reduce;

$f2 = Formula("$a2/$u2");

#### Answers ####
# for AnswerHints, use array ref: [ message, option => value ]
# can use the same message and partial credit for both a & b
$partial_credit = [
    "Make sure to remember absolute values when integrating \(\displaystyle \int \frac{1}{u}\,du\)",
    score => 0.5
];

# set the domain for x to check both sides of the asymptote
$limits = [ -$a1 - 5, -$a1 + 5 ];
Context('Numeric')->variables->are(x => [ 'Real', limits => $limits ]);

# use test_at to ensure that at least one input where the
# absolute value makes a difference gets checked
$F1 = FormulaUpToConstant("ln|$u1|")->with(test_at => [ [ -$a1 - 0.5 ] ]);

# many students will forget the absolute values, use AnswerHints
# $incorrect_answer => [message_string, %options]
$F1_partial = FormulaUpToConstant("ln($u1)");
@hints1     = AnswerHints($F1_partial => $partial_credit);

# set the domain for x to check both sides of the asymptote
$l      = -$c2 / $b2;
$limits = [ $l - 5, $l + 5 ];
Context('Fraction')->variables->are(x => [ 'Real', limits => $limits ]);
Context()->flags->set(reduceConstants => 0);

$F2_coeff = Fraction($a2, $b2)->reduce;
$F2       = FormulaUpToConstant("$F2_coeff ln|$u2|")
    ->with(test_at => [ [ $l - 2 / $b2 ] ]);

# again, incorrect => [message, %options]
$F2_partial = FormulaUpToConstant("$F2_coeff ln($u2)");
@hints2     = AnswerHints($F2_partial => $partial_credit);

#################

BEGIN_PGML
Evaluate the indefinite integrals using substitution. (Use [`C`]
for the constant of integration.)

a) [` \displaystyle \int [$f1] \, dx = `]
[_]{$F1->cmp->withPostFilter(@hints1)}{40}

b) [` \displaystyle \int [$f2] \, dx = `]
[_]{$F2->cmp->withPostFilter(@hints2)}{40}
END_PGML

BEGIN_PGML_SOLUTION
a) Substitute [`u = [$u1]`].  Then [`du = 1 \,dx`].

    [```
      \begin{array}{rcl}
        \displaystyle  \int [$f1] \, dx &=&
        \displaystyle \int \frac{1}{u} \,du \\
        &=& \ln |u| + C\\
        &=& \ln |[$u1]| = C.
      \end{array}
    ```]

b) Substitute [`u = [$u2]`].  Then [`du = [$b2] \,dx`].

    [```
      \begin{array}{rcl}
        \displaystyle  \int [$f2] \, dx &=&
        \displaystyle \dfrac{1}{[$b2]} \int \dfrac{[$a2]}{u} \, du \\
        &=& \dfrac{1}{[$b2]} \cdot [$a2] \ln |u| + C\\
        &=& [$F2_coeff] \ln |[$u2]| + C\\
      \end{array}
    ```]
END_PGML_SOLUTION

ENDDOCUMENT();

