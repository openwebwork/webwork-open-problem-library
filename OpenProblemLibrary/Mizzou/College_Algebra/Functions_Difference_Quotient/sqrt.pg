# DESCRIPTION
# Find the difference quotient of f(x) = sqrt(x-a).
# ENDDESCRIPTION

## Author1('David Covert')
## Author2('Teri Christiansen')
## Author3('Rick Lynch')
## DBsubject(Algebra)
## DBchapter(Functions)
## DBsection(Difference quotient)
## Institution(University of Missouri-Columbia)
## MLT(DiffQuotDirect_nonpoly)
## Level(3)
## KEYWORDS('functions', 'difference quotient', 'composition')


###########################################################################
# initialization 
###########################################################################
DOCUMENT();
loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "parserMultiAnswer.pl",
  "contextLimitedPowers.pl",
  "unionTables.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());
$showPartialCorrectAnswers = 1;


###########################################################################
# setup contexts and variables 
###########################################################################
Context("Numeric");
Context()->variables->are(x=>"Real", h=>"Real", "X"=>"Real", H=>"Real");
LimitedPowers::OnlyPositiveIntegers();
$a = non_zero_random(-7,7);
Context()->variables->set(x=>{limits=>[10,12]});
Context()->variables->set(h=>{limits=>[10,12]});
$f = "\displaystyle \sqrt{x-$a}";

$num = Formula("1")->reduce;
$den = Formula("sqrt(x+h - $a) + sqrt(x-$a)")->reduce;
$answer = MultiAnswer($num, $den)->with(
  singleResult => 0,
  allowBlankAnswers => 1,
  checker => sub {
    my ( $correct, $student, $self ) = @_;
    my ( $c1, $c2 ) = @{$correct};
    my ( $s1, $s2 ) = @{$student};
    my ( $r1, $r2 ) = (0,0);
    $s1 = Formula($s1) unless ($s1->type eq 'Formula' || $s1->type eq 'String');
    $s2 = Formula($s2) unless ($s2->type eq 'Formula' || $s2->type eq 'String');
    $s1 = Formula("x^{1000}") if ($s1->type eq 'String'); #bogus answer
    $s2 = Formula("x^{500}") if ($s2->type eq 'String'); #bogus answer
    $r1 = 1 if ($c1 == $s1);
    $r2 = 1 if ($c2 == $s2);
    ($r1,$r2) = (1,1) if ($c1 == -$s1 && $c2 == -$s2);
    if ($c1*$s2 == $c2*$s1 && $c1 != $s1 && $c1 != -$s1) {
      $self->setMessage(1,"Your answer can be simplified further");
      $self->setMessage(2,"Your answer can be simplified further");
    }
    return [$r1,$r2];
  }
);
$texanswer = "\(\displaystyle \frac{1}{\sqrt{x+h-$a} + \sqrt{x-$a}}\)";

$frac = "\frac{f(x+h) - f(x)}{h}";
if ($displayMode eq 'TeX') {
  $displayfrac =
  "\[ $frac = ".$answer->ans_rule(10).$answer->ans_rule(10)." \]";
} else {
  $displayfrac =
  ColumnTable(
  "\( \displaystyle $frac = \)",
  $answer->ans_rule(20).$BR.$HR.$answer->ans_rule(20),
  indent => 0, separation => 10, valign => "MIDDLE"
  );
}


###########################################################################
# state the problem 
###########################################################################
Context()->texStrings;
BEGIN_TEXT
Find the difference quotient of the function
$PAR
\[
f(x) = $f
\]
$PAR
and simplify your answer as much as possible. 
$BCENTER
$displayfrac
$ECENTER
END_TEXT
Context()->normalStrings;


###########################################################################
# check the answer  
###########################################################################
ANS($answer->cmp());


COMMENT('Find the difference quotient of f(x) = sqrt(x-a).');
ENDDOCUMENT();
