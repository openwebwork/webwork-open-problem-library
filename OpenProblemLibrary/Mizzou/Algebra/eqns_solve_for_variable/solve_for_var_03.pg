# DESCRIPTION
# Solve for a variable by getting the exponent on one side
# and then getting rid of the exponent by taking an nth root.
# ENDDESCRIPTION

## DBsubject(Algebra)
## DBchapter(Polynomial equations and functions)
## DBsection(Polynomial equations)
## Institution(University of Missouri-Columbia)
## Author(Patrick Spencer)
## MLT(linearInPoly)
## MLTleader(1)
## Level(2)
## KEYWORDS('equations', 'rational')


##################################################################
# initialization 
##################################################################
DOCUMENT();
loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "contextFraction.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());
$showPartialCorrectAnswers = 1;


###########################################################################
# setup contexts and variables 
###########################################################################
Context("Numeric");
@vlist = qw(x y z t u v);
$var = list_random(@vlist);
Context()->variables->are($var=>"Real");
$exp = random(3,5);
if ($exp == 3) {
  $base_sign = random(-1,1,2);
  $base_abs_value = random(2,5);
} elsif ($exp == 4) {
  $base_sign = 1;
  $base_abs_value = random(2,3);
} elsif ($exp == 5) {
  $base_sign = random(-1,1,2);
  $base_abs_value = random(2,3);
}
$base = $base_sign*$base_abs_value;
$base_nth_power = $base**$exp;
do {$b = random(-1,1,2)*random(2,7)} until ($b != $base);
$ans_numer = $base - $b;
$ans_numer_abs_value = abs($ans_numer);

# Make sure final answer isn't 1 or -1. Not sure why. It just looks
# nicer.
do {$m = random(2,7)} until (($ans_numer_abs_value/$m) != 1);
$eq_disp = "($m $var+ $b)^$exp - $base_nth_power = 0";

# We choose ans sign so we can write the fraction with the sign
# out in front
Context("Fraction");
if ($exp == 4) {
  $plus_ans = Compute("($base-$b)/$m");
  $min_ans = Compute("(-$base-$b)/$m");
  $ans = List($plus_ans,$min_ans);
} else {
  $ans = Compute("$ans_numer/$m");
} 

###########################################################################
# state the problem 
###########################################################################
Context()->texStrings;
BEGIN_TEXT
Solve for \($var\):
$PAR \[$eq_disp\] $PAR
\( $var = \) \{ans_rule(20)\}
END_TEXT
Context()->normalStrings;


###########################################################################
# check the answer  
###########################################################################
ANS($ans->cmp());


COMMENT('The exponent is randomly chosen between 3, 4, and 5. 
  The part inside the parenthesis is always a linear function with a 
  positive slope. The variable is random.');
ENDDOCUMENT();

