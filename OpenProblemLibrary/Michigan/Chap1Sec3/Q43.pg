# DESCRIPTION
# Problem from Calculus, single variable, Hughes-Hallett et al., 4th ed.
# original WeBWorK problem written by Aaron Wooton, <wootton@up.edu>
# completely rewritten by Gavin LaRose, <glarose@umich.edu> to use 
# piecewise linear functions

## Tagged by glr 10/23/08

## Author('Gavin LaRose');
## DBsubject(Algebra)
## DBchapter(Functions)
## DBsection(Compositions and combinations of functions)
## Institution(University of Michigan)
## MLT(ccf-graphs-evaluate-comp)
## MLTleader(1)
## Level(2)
## MO(1)
## TitleText1('Calculus')
## TitleText2('Calculus')
## TitleText3('Calculus')
## TitleText4('Applied Calculus')
## TitleText5('Calculus')
## AuthorText1('Hughes-Hallett')
## AuthorText2('Hughes-Hallett')
## AuthorText3('Hughes-Hallett')
## AuthorText4('Hughes-Hallett')
## AuthorText5('Hughes-Hallett')
## EditionText1('4')
## EditionText2('5')
## EditionText3('6')
## EditionText4('4')
## EditionText5('7')
## Section1('1.3')
## Section2('1.3')
## Section3('1.3')
## Section4('1.8')
## Section5('1.3')
## Problem1('43')
## Problem2('45')
## Problem3('57')
## Problem4('19')
## Problem5('43')
## KEYWORDS('calculus','derivative')

## Textbook tags
## HHChapter1('A Library of Functions')
## HHChapter2('A Library of Functions')


DOCUMENT();
loadMacros(
  "PGstandard.pl",
  "PGchoicemacros.pl",
  "PGgraphmacros.pl",
  "MathObjects.pl",
  "PGcourse.pl"
);

Context("Numeric");

TEXT(beginproblem());
$showPartialCorrectAnswers = 1;

# the problem from the book in this case uses the functions
#   x^3 - a x^2 + b x - c, with a, b and c integers chosen from (0,3], and
#   e sin(pi x / f), with e either .5 or 1 and f an integer in [1,4]
# we have trouble with this because we're demanding that students
# estimate values of f(g(x)) and g(f(x)), which are hard to read from
# the graphs.  so we'll use a pair of piecewise continuous functions
# instead.

# f is defined on [-2,3], and takes on values in [-10,10].  We make it 
# a piecewise linear semi-cubic, somewhat thus:
#           b_c  /f
#           / \_/    (or the negative of this)
#         a/  d e
# the points a-e are
$sgn = list_random(-1,1);
@a = (-2, $sgn*10);
$bcy = -1*$sgn*random(1,6,2);
@b = (-1, $bcy);  @c = (0, $bcy);
$dey = $sgn*random(1,6,2);
@d = (1, $dey);   @e = (2, $dey);
@f = (3,-1*$sgn*10);
# then the function f is given by
@fn = ();
$fn[0] = Formula("(x+2)*($b[1]-$a[1]) + $a[1]");  # for x <= -1
$fn[1] = Formula("$bcy");                         # for x <= 0
$fn[2] = Formula("x*($d[1]-$c[1]) + $bcy");       # for x <= 1
$fn[3] = Formula("$dey");                         # for x <= 2
$fn[4] = Formula("(x-2)*($f[1]-$e[1]) + $dey");   # for x <= 3

$graphf = init_graph(-2,-10,3,10,axes=>[0,0],grid=>[5,20],size=>[250,250]);
plot_functions( 
    $graphf, 
    "$fn[0] for x in <-2,-1] using color:blue and weight:2",
    "$fn[1] for x in [-1,0] using color:blue and weight:2",
    "$fn[2] for x in [0,1] using color:blue and weight:2",
    "$fn[3] for x in [1,2] using color:blue and weight:2",
    "$fn[4] for x in [2,3> using color:blue and weight:2",
);

# the description of this is
$fdesc = "graph of a piecewise linear function between ($a[0],$a[1]), " .
    "($b[0],$b[1]), ($c[0],$c[1]), ($d[0],$d[1]), ($e[0],$e[1]), and " .
    "($f[0],$f[1]).";

# g is a sinusoidal creature.  we graph a piecewise thing with 
# amplitude between .5 and 2 (step .5) and period of 4, 6 or 12
$A = random(.5,2,.5);
$P = list_random(4,6,12);

# build the functions
# the slope of the angled pieces is
if ( $P == 4 ) { $m = 2*$A; }
elsif ( $P == 6 ) { $m = $A; }
else { $m = $A/2; }

$xval = -12;
$yval = -1*$sgn*$A;

@gn = ();  @gchng = ();  # @gchng are the x values where we change
while ( $xval < 12 ) {   #    between functions in @gn
    push( @gn, Formula("(x-$xval)*$sgn*$m + $yval") );
    $yval *= -1;  $sgn *= -1;
    push( @gn, Formula("$yval") );
    if ( $P == 4 ) {
	push( @gchng, $xval+1, $xval+2, $xval+3, $xval+4 );
	$xval += 2;
	push( @gn, Formula("(x-$xval)*$sgn*$m + $yval") );
	$xval += 2;
    } elsif ( $P == 6 ) {
	push( @gchng, $xval+2, $xval+3, $xval+5, $xval+6 );
	$xval += 3;
	push( @gn, Formula("(x-$xval)*$sgn*$m + $yval") );
	$xval += 3;
    } else {
	push( @gchng, $xval+4, $xval+6, $xval+10, $xval+12 );
	$xval += 6;
	push( @gn, Formula("(x-$xval)*$sgn*$m + $yval") );
	$xval += 6;
    }
    $yval *= -1;  $sgn *= -1;
    push( @gn, Formula("$yval") );
}

$yM = int($A) + 1; $ygrid = 4*$yM;
$graphg = init_graph(-12,-$yM,12,$yM,axes=>[0,0],grid=>[24,$ygrid],
		     size=>[250,250]);

plot_functions( 
    $graphg, 
    "$gn[0] for x in <-12,$gchng[0]] using color:red and weight:2"
);
for ( my $i=1; $i<@gn-1; $i++ ) {
    plot_functions(
	$graphg,
	"$gn[$i] for x in [" . $gchng[$i-1] . 
	    ",$gchng[$i]] using color:red and weight:2" 
    );
}
plot_functions( 
    $graphg, 
    "$gn[-1] for x in [$gchng[-2],12> using color:red and weight:2"
);

# a description of this function
$gdesc = "graph of a piecewise linear function connecting the points " .
    "(-12," . $gn[0]->eval(x=>-12) . "), ";
for ( my $i=0; $i<@gchng-1; $i++ ) {
    $gdesc .= " ($gchng[$i]," . $gn[$i]->eval(x=>$gchng[$i]) . "), ";
}
$gdesc .= "and (12," . $gn[-1]->eval(x=>12) . ")";

# whew!  now, pick the points at which to evaluate the functions
$q1=random(-10,10,1);
$q2=random(-1,2,1);
$q3=random(-10,10,1);
# we're finding f(g(q1)), g(f(q2)) and g(g(q3)), which is
#   f(r1), g(r2) and g(r3)
# to find values of g we have to know the index in the array of functions
$ind1 = 0;  $ind3 = 0;
while ( $q1 > $gchng[$ind1] ) { $ind1++; }
while ( $q3 > $gchng[$ind3] ) { $ind3++; }
$r1 = $gn[$ind1]->eval(x=>$q1);
$r2 = $fn[$q2+1]->eval(x=>$q2);
$r3 = $gn[$ind3]->eval(x=>$q3);
# so the answers are
$ind2 = 0;  $ind4 = 0;
while ( $r2 > $gchng[$ind2] ) { $ind2++; }
while ( $r3 > $gchng[$ind4] ) { $ind4++; }
$a1 = $fn[int($r1+2)]->eval(x=>$r1);  # $r1 = x-value, -2 to 2; +2=>index of f
$a2 = $gn[$ind2]->eval(x=>$r2);
$a3 = $gn[$ind4]->eval(x=>$r3);

Context()->texStrings;
BEGIN_TEXT
$BCENTER
\{ begintable(2) \}
\{ row( image(insertGraph($graphf),tex_size=>300, height=>250, width=>250,
	      extra_html_tags=>'alt="' . $fdesc . '"'),
        image(insertGraph($graphg),tex_size=>300, height=>250, width=>250,
	      extra_html_tags=>'alt="' . $gdesc . '"') ) \}
\{ row( "\(f(x)\)", "\(g(x)\)" ) \}
\{ endtable() \}
$ECENTER
$PAR
Use the figures above, which show the functions \(f(x)\) and \(g(x)\),
to find the following values. Note that you can find exact values.

$BR
${BBOLD}1.$EBOLD \(f(g($q1))\)=\{ans_rule(30) \}
$BR
${BBOLD}2.$EBOLD \(g(f($q2))\)=\{ans_rule( 30) \}
$BR
${BBOLD}3.$EBOLD \(g(g($q3))\)=\{ans_rule( 30) \}

END_TEXT
Context()->normalStrings;

ANS( Compute( $a1 )->cmp() );
ANS( Compute( $a2 )->cmp() );
ANS( Compute( $a3 )->cmp() );

Context()->texStrings;
SOLUTION(EV3(<<'END_SOLUTION'));
$PAR SOLUTION $PAR

${BBOLD}1.$EBOLD
\(f(g($q1)) = f($r1)\) which is \($a1\).
$PAR

${BBOLD}2.$EBOLD
\(g(f($q2)) = g($r2)\) which is \($a2\).
$PAR

${BBOLD}3.$EBOLD
\(g(g($q3)) = g($r3)\) which is \($a3\).

END_SOLUTION
Context()->normalStrings;

;

ENDDOCUMENT();
