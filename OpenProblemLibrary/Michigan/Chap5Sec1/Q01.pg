# DESCRIPTION
# Problem from Calculus, single variable, Hughes-Hallett et al., 4th ed.
# WeBWorK problem written by Gavin LaRose, <glarose@umich.edu>

## Tagged by glr 02/08/09

## DBsubject(Calculus - single variable)
## DBchapter(Applications of integration)
## DBsection(Distance, velocity, acceleration)
## Institution(University of Michigan)
## Author(Gavin LaRose)
## Level(2)
## TitleText1('Calculus')
## TitleText2('Calculus')
## TitleText3('Calculus')
## TitleText4('Applied Calculus')
## AuthorText1('Hughes-Hallett')
## AuthorText2('Hughes-Hallett')
## AuthorText3('Hughes-Hallett')
## AuthorText4('Hughes-Hallett')
## EditionText1('4')
## EditionText2('5')
## EditionText3('6')
## EditionText4('4')
## Section1('5.1')
## Section2('5.1')
## Section3(5.Review)
## Section4('5.1')
## Problem1('1')
## Problem2('3')
## Problem3(1)
## Problem4('3')
## KEYWORDS('calculus', 'integral', 'definite integrals', 'distance')

## Textbook tags
## HHChapter1('Key Concept: The Definite Integral')
## HHChapter2('Key Concept: The Definite Integral')

DOCUMENT();
loadMacros(
  "PGstandard.pl",
  "PGchoicemacros.pl",
  "PGgraphmacros.pl",
  "MathObjects.pl",
  "PGcourse.pl"
);
 
Context("Numeric");

TEXT(beginproblem());
$showPartialCorrectAnswers = 1;

# generate some data for the velocity; the original data were
# 88 45 16 0 (note 45 = 51% of 88, 16 = 36% of 45)

$v0 = random(80,100,1);
@vdata = ( $v0, round(0.51*$v0), round(0.51*0.36*$v0), 0);

$lower = 2*($vdata[1] + $vdata[2] + $vdata[3]);
$upper = 2*($vdata[0] + $vdata[1] + $vdata[2]);

Context()->texStrings;
BEGIN_TEXT

A car comes to a stop six seconds after the driver applies the brakes.
While the brakes are on, the following velocities are recorded:
$BR
$BCENTER
\{ begintable(5) \}
\{ row( 'Time since brakes applied (sec)', 0, 2, 4, 6 ) \}
\{ row( 'Velocity (ft/s)', @vdata ) \}
\{ endtable() \}
$ECENTER
$PAR

Give lower and upper estimates (using all of the available data) for
the distance the car traveled after the brakes were applied.$BR
lower: \{ ans_rule(15) \} $BR
upper: \{ ans_rule(15) \} $BR
(for each, include \{ helpLink("units") \})

$PAR
${BITALIC}On a sketch of velocity against time, show the lower and upper
estimates you found above.$EITALIC.

END_TEXT
Context()->normalStrings;

ANS(num_cmp($lower, 'units'=>'ft') );
ANS(num_cmp($upper, 'units'=>'ft') );

# generate a figure that illustrates the lower and upper estimates
# a smooth curve through the points given is 
$vel = "$vdata[0] + (-13*$vdata[0] + 27*($vdata[1] - $vdata[2]))*x/12 + " .
    "(58*$vdata[0] - 189*$vdata[1] + 270*$vdata[2])*(x^2)/144 + " . 
    "(-1*$vdata[0] + 4*$vdata[1] - 7*$vdata[2])*(x^3)/16 + " .
    "(2*$vdata[0] - 9*$vdata[1] + 18*$vdata[2])*(x^4)/576";

$ymax = ( $v0 > 94 ) ? 105 : 95;
$ygrid = ( $ymax == 105 ) ? 11 : 10;
$gr1 = init_graph(-1,-10,7,$ymax, 'axes'=>[0,0], 'grid'=>[8,$ygrid]);
$gr2 = init_graph(-1,-10,7,$ymax, 'axes'=>[0,0], 'grid'=>[8,$ygrid]);

# add the smooth curve
add_functions( $gr1, "$vel for x in <0,6> using color:black and weight:1" );
add_functions( $gr2, "$vel for x in <0,6> using color:black and weight:1" );

# add the boxes for the lower and upper estimates
$xval = 0;
$gr1->moveTo(0,0);  
$gr2->moveTo(0,0);
for ( my $i=0; $i<3; $i++ ) {
    $gr1->lineTo($xval, $vdata[$i+1], 'blue');
    $gr1->lineTo($xval+2, $vdata[$i+1], 'blue');
    $gr1->lineTo($xval+2, 0, 'blue');
    $gr2->lineTo($xval, $vdata[$i], 'blue');
    $gr2->lineTo($xval+2, $vdata[$i], 'blue');
    $gr2->lineTo($xval+2, 0, 'blue');
    $xval += 2;
}

# finally, add the points
$xval = 0;
for $yval ( @vdata ) {
    $gr1->stamps( closed_circle($xval, $yval, 'black') );
    $gr2->stamps( closed_circle($xval, $yval, 'black') );
    $xval += 2;
}

Context()->texStrings;
BEGIN_SOLUTION
$PAR SOLUTION $PAR

To find a lower estimate for the distance the car has traveled, we use the
lowest velocity for each of the intervals \(0\le t\le 2\), \(2\le t\le 4\), 
and \(4\le t\le 6\) to calculate an estimated distance traveled in each
interval.  This gives 
\[ 
\mbox{dist} = ($vdata[1])(2) + ($vdata[2])(2) + ($vdata[3])(2) = $lower.
\]
Similarly, for the upper estimate, we take the maximum velocity for each
interval, to get 
\[ 
\mbox{dist} = ($vdata[0])(2) + ($vdata[1])(2) + ($vdata[2])(2) = $upper.
\]

$PAR

Graphically, each of the terms in these sums is the area of a rectangle,
as shown outlined in blue in the figures below.  The lower- and
upper estimates are the sum of the areas of these rectangles.  (Note
that the height of the last rectangle for the lower estimate is zero,
so it does not contribute to the sum.)
$PAR
$BCENTER
\{ begintable(2) \}
\{ row( image(insertGraph($gr1), 'tex_size'=>300),
	image(insertGraph($gr2), 'tex_size'=>300) ) \}
\{ row( 'lower estimate', 'upper estimate' ) \}
\{ endtable() \}
${BITALIC}(Click on the graphs to see a larger version.)$EITALIC
$ECENTER

END_SOLUTION
Context()->normalStrings;


;
ENDDOCUMENT();

