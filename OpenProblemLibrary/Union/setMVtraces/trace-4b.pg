## DESCRIPTION
##   Relate Trace Graphs to Equations for a Function
## ENDDESCRIPTION

## Tagged by nhamblet

## DBsubject(Calculus - multivariable)
## DBchapter(Concepts for multivariable functions)
## DBsection(Traces, contours, and level sets)
## Date(8/23/07)
## Institution(Union College)
## MLT(tcl_traces_recover_fn)
## Level(2)
## KEYWORDS('Trace', 'Level Curve', 'Multivariate', 'Multivariable')

DOCUMENT();        # This should be the first executable line in the problem.

loadMacros(
  "PGstandard.pl",
  "PGunion.pl",
  "MathObjects.pl",
  "PGgraphmacros.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());

##############################################
#  Setup

Context("Numeric")->variables->are(
  x=>'Real', y=>'Real',
  a=>'Real', b=>'Real',
);

#
#  The function to plot
#
$c = non_zero_random(-1,1,1);
$a = $c*random(2,5,1)/2;
$b = -$c*random(2,5,1)/2;
$f = Formula("a x^2 y + b x y^2");
$f->perlFunction('f',['x','y']);

#
#  Traces to show
#
$x1 = non_zero_random(-2,2,1); $x1 /= 2 if (abs($b) >= 2 && abs($x1) == 2);
$x2 = non_zero_random(-2,2,1); $x2 /= 2 if (abs($a) >= 2 && abs($x2) == 2);

$x = max(.5,min(3,round(-2*$b*$x2/$a)/2));
$y = max(.5,min(3,round(-2*$a*$x1/$b)/2));

#
#  Points to show
#
$xv = round(-$b*$y/$a/2); $xv = 1 if ($xv == 0);
$fxv = f($xv,$y); if (abs($fxv) < .75) {$xv = -$xv; $fxv = f($xv,$y)}

$yv = round(-$a*$x/$b/2); $yv = -1 if ($yv == 0);
$fyv = f($x,$yv); if (abs($fyv) < .75) {$yv = -$yv; $fyv = f($x,$yv)}

$M = int(max(abs($fxv),abs($fyv),4))+1;
#
#  Graph size
#
($xm,$xM) = (-3,3);
($ym,$yM) = (-3,3);
($zm,$zM) = (-$M,$M);
$size = [200,250];
$tex_size = 350;

##############################################

#
#  The plot defaults
#
@Goptions = (
  $ym,$zm,$yM,$zM,                            # dimensions of graph
  axes => [0,0], grid => [$yM-$ym,$zM-$zm],   # number of grid lines
  size => $size                               # pixel dimension
);
@imageoptions = (size=>$size, tex_size=>$tex_size, border=>0);

$plotoptions = "using color:red and weight=2";

#
#  Make the traces
#
$Gx = init_graph(@Goptions);
$fx = $f->substitute(a=>$a,b=>$b,x=>$x,y=>'x');
plot_functions($Gx,
  "$fx for x in <$ym,$yv] $plotoptions",
  "$fx for x in <$yv,$yM> $plotoptions",
);
$Xtrace = Image($Gx,@imageoptions);

$Gy = init_graph(@Goptions);
$fy = $f->substitute(a=>$a,b=>$b,y=>$y);
plot_functions($Gy,
  "$fy for x in <$xm,$xv] $plotoptions",
  "$fy for x in <$xv,$xM> $plotoptions",
);
$Ytrace = Image($Gy,@imageoptions);

Context()->texStrings;
#
#  Make the table of images
#
$Images =
  BeginTable(spacing=>3).
    AlignedRow([$Xtrace,$Ytrace]).
    AlignedRow(["Trace for \(x=$x\) has",    "Trace for \(y=$y\) has"]).
    AlignedRow(["a point at \(($yv,$fyv)\).","a point at \(($xv,$fxv)\)."]).
  EndTable();
Context()->normalStrings;

##############################################
#  Main text

Context()->texStrings;
BEGIN_TEXT

The graphs below are traces for a function at \(x=$x\) and
\(y=$y\).
$PAR

$Images
$PAR

If \($f\) then
\(a\) = \{ans_rule(6)\} and \(b\) = \{ans_rule(6)\}.

END_TEXT
Context()->normalStrings;

##################################################
#  Answers

ANS(num_cmp($a));
ANS(num_cmp($b));

$showPartialCorrectAnswers = 1;

##################################################


;
ENDDOCUMENT();
