# DESCRIPTION
# The derivative as a rate of change
# ENDDESCRIPTION


## Author('Paul Pearson');
## DBsubject(Calculus - single variable)
## DBchapter(Applications of differentiation)
## DBsection(Rates of change - engineering and physics)
## Institution(Hope College)
## MLT(DCvelocity_acceleration_position_from_graph)
## Level(3)
## TitleText1('Thomas Calculus Early Transcendentals')
## AuthorText1('Thomas, Weir, Hass')
## EditionText1('12')
## Section1('3.4')
## Problem1('16')
## KEYWORDS('rate of change','displacement','velocity','acceleration')


################################
#  Initialization

DOCUMENT();

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "AnswerFormatHelp.pl",
  "PGgraphmacros.pl",
  "unionTables.pl",
  "contextInequalities.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());

$refreshCachedImages = 1;


################################
#  Setup

Context("Inequalities")->variables->are(t=>'Real');
Context()->flags->set(formatStudentAnswer=>'parsed');

################################################
# 
#  The horizontal line plot (image of s)
#

$gr[0] = init_graph(-1,-5,1,5, size=>[68,300]);
# t-axis
$gr[0] -> moveTo(0,-6);
$gr[0] -> lineTo(0,6,'black','1');

$gr[0] -> v_ticks(0,'black',-5,-4,-3,-2,-1,0,1,2,3,4,5);
#$gr[0]->lb( new Label(-0.5,0,'0', 'black','center','middle'));
foreach my $y (-4..4) {
  $gr[0]->lb( new Label(-0.5,$y,$y, 'black','center','middle'));
}

$gr[0]->lb( new Label(-0.25,4.5,'y (cm)', 'black','left','middle'));

$gr[0] -> moveTo(0,-0.5);
$gr[0] -> arrowTo(0,-1.5,'blue',3);

$gr[0] -> stamps( closed_circle(0,0.5,'blue') );
$gr[0] -> lb( new Label(0.5,0.5,'P', 'black','center','middle'));

$gr[0] -> moveTo(0,1.5);
$gr[0] -> arrowTo(0,2.5,'blue',3);



##################################################
#
#  Values (t,y) for the piecewise linear function
# 

@t = (0..6);

@y = ();
$y[0] = 0;
$y[1] = random(-1,1,2) * random(1,4,1);
$y[2] = $y[1];
$t2 = random(2,3,1);
$t3 = $t2 + 1;
if ( $t2 == 2 ) { 
  $y[3] = -1*$y[1]; 
  $y[4] = $y[3];
  $y[5] = $y[3];
} else { 
  $y[3] = $y[2];
  $y[4] = -1*$y[1];
  $y[5] = $y[4]; 
}
$y[6] = 0;

#
#  Graph canvas
#
$gr[1] = init_graph(-1,-5,7,5,axes=>[0,0],grid=>[8,10],size=>[300,300]);
$gr[1] -> lb('reset');

#
#  Piecewise linear plot (graph of s)
#
foreach my $i (0..5) {
  $gr[1]->moveTo($t[$i],$y[$i]);
  $gr[1]->lineTo($t[$i+1],$y[$i+1],'blue',3);
}

#
#  Graph labels and functions
#
$gr[1] -> lb(new Label ( 5.5,0,'t (sec)','black','left','bottom'));
$gr[1] -> lb(new Label ( 0.25,4.5,'y (cm)','black','left','bottom'));
$gr[1] -> lb(new Label ( 4,4.5,'y = f(t)','black','left','bottom'));
foreach my $x (1..6) {
  $gr[1] -> lb(new Label ( $x,-0.5, $x,'black','center','middle'));
}
foreach my $y (1..4) {
  $gr[1] -> lb(new Label (-0.5, $y, $y,'black','center','middle'));
  $gr[1] -> lb(new Label (-0.5,-$y,-$y,'black','center','middle'));
}



$fig[0] = image(insertGraph($gr[0]), width=>68, height=>300, tex_size=>158);
$fig[1] = image(insertGraph($gr[1]), width=>300, height=>300, tex_size=>700);


################################
#  Answers

if (sgn($y[1]) > 0) {

  $up = Compute("0 < t < 1 or 5 < t < 6");
  $still = Compute("1 < t < $t2 or $t3 < t < 5 ");
  $velneg = Compute("$t2 < t < $t3");

} else {

  $up = Compute("$t2 < t < $t3");
  $still = Compute("1 < t < $t2 or $t3 < t < 5 ");
  $velneg = Compute("0 < t < 1 or 5 < t < 6");

}



################################
#  Main text

Context()->texStrings;
BEGIN_TEXT
\{
ColumnTable(
"A particle \( P \) moves on the number line
shown in figure A.  The position of \( P \)
as a function of time \( t \) is shown in
figure B.  (Note: The graph of  
\( y = f(t) \) shown in figure B is ${BITALIC}not${EITALIC}
the path of the particle; it is a plot of 
the particle's position versus time.)
$BR
$BR
(a) When is \( P \) moving up? " . 
ans_rule(20).$SPACE.
AnswerFormatHelp("inequalities","help (intervals or inequalities)").
"$BR
$BR
(b) When is \( P \) standing still? ".
ans_rule(20).$SPACE.
AnswerFormatHelp("inequalities","help (intervals or inequalities)").
"$BR
$BR
(c) When is the velocity of \( P \) negative? ".
ans_rule(20).$SPACE.
AnswerFormatHelp("inequalities","help (intervals or inequalities)").
"$BR
$BR
(d) On a sheet of paper, graph the particle's velocity and speed 
(where defined)."
,
BeginTable().
  AlignedRow([$fig[0],$fig[1]]).
  TableSpace(5,0).
  AlignedRow(["A","B"]).
EndTable()
,
indent => 0, separation => 30, valign => "TOP"
)
\}
END_TEXT
Context()->normalStrings;


#################################
#  Answer evaluation

$showPartialCorrectAnswers = 1;

ANS( $up->cmp() );
ANS( $still->cmp() );
ANS( $velneg->cmp() );



ENDDOCUMENT();
